	.TITLE BBFLY
	.INCLUDE BBRAM
	.INCLUDE VGMC16
;
;      *******************************************************
;      * COPYRIGHT 1982 ATARI. UNAUTHORIZED REPRODUCTION,    *
;      * ADAPTATION, DISTRIBUTION, PERFORMANCE OR DISPLAY OF *
;      * THIS COMPUTER PROGRAM OR THE ASSOCIATED AUDIOVISUAL *
;      * WORK IS STRICTLY PROHIBITED!!!!!!!                  *
;      *******************************************************
;
; EXTERNAL REFERENCES
;
	.GLOBL SCVIAT	;10000 POINTS FOR ESCAPE FROM TBUGS
	.GLOBL SHWCIR
	.GLOBL PNTRNG
	.GLOBL SNDOE
	.GLOBL NFLPLY	;FOR MUTANT SWATTING
	.GLOBL SCLINK	;LINKS FOR FALLING SCORES
	.GLOBL DIST2R	;WEB COLLISIONS
	.GLOBL BITABL	;BIT TABLE LOOKUP
	.GLOBL DSCORE	;SHOW TRIALX AS SCORE
	.GLOBL ANTSCR	;SCORE/TAG DISPLAY
	.GLOBL SNDPP	;PLAYER SHOOT SOUND
	.GLOBL SNDEG	;EGG SHOVED OFF BRINK
	.GLOBL OBJTYP	;ATTRIBUTE TABLE
	.GLOBL WIGGLE	;ANIMATION SEQUENCING
	.GLOBL FITWEB	;OBJECT/WEB-RING TEST
	.GLOBL CENTER
	.GLOBL SNDOB	;OBJECT BUMPS WALL
	.GLOBL FITMOR	;FITWEB CONTINUED
	.GLOBL SCORIT	;ADD SCORE
	.GLOBL VGHEXZ	;VG NUMERIC OUTPUT
	.GLOBL SNDOF	;SOUND OF FALLING
	.GLOBL SNDOX	;SECONDARY EXPLOSION SOUND
	.GLOBL INISOU	;ALL SOUNDS OFF
;
; GLOBAL DEFINITIONS
;
	.GLOBL SESCRE	;FALLING SCORES
	.GLOBL FLYTRN
	.GLOBL FLYMUV
	.GLOBL MUTMUV
;
;
; PONGER IS AZIMUTH*2 OF WALL SECTIONS BY ARM NUMBER
;
PONGER:	.BYTE 41.,54.,0,9,22.,32.
	.BYTE 41.,54.,0,9,22.,32.
;
;
;
; DIST2R IS 3 BYTES/ENTRY
; 1ST ENTRY IS X & Y+X/2 COLLISION (ARM 2 AND 0)
; 2ND IS Y COLLISION ARM 11
; 3RD IS Y+2X COLLISION ARM 1
;
DIST2R:
N	= 1
	.REPT 8
	.WORD <50.*N>*40,<40.*N>*40,<110.*N>*40
N	= N+1
	.ENDR
;
;OBJECT MOVEMENT DELTA X & Y BY HEADING/SPEED
; NOTE THAT ALL OBJECT SHARE THE SAME TABLES, EVEN THO
; THE PLAYERS HEADING IS ROUNDED TO NEAREST 45 MULTIPLE
; UNLIKE THE 'REAL' BLACK WIDOW BOARD WHICH HANDLED REFLECTIONS
; AND COULD NOT REPRESENT A TRUE NORTH-SOUTH OR EAST-WEST
; OR 45 DEGREE PICTURE OF A BUG, THIS VERSION ALLOWS MOVEMENT
; IN THOSE DIRECTIONS AS NO REFLECTION TAKES PLACE
;
	.MACRO DXY DX,DY
	...S = 1
	.REPT 16.
	.WORD DX*...S*6,DY*...S*6
	...S = ...S+1
	.ENDR
	.ENDM
MDLTAS:
	DXY 0,15.	;HDG 00 :  0.000 DEGREES
	DXY 1,15.	;HDG 01 :  5.625 DEGREES
	DXY 3,15.	;HDG 02 : 11.250 DEGREES
	DXY 4,14.	;HDG 03 : 16.875 DEGREES
	DXY 6,14.	;HDG 04 : 22.500 DEGREES
	DXY 7,13.	;HDG 05 : 28.125 DEGREES
	DXY 8,12.	;HDG 06 : 33.750 DEGREES
	DXY 10.,12.	;HDG 07 : 39.375 DEGREES
	DXY 11.,11.	;HDG 08 : 45.000 DEGREES
	DXY 12.,10.	;HDG 09 : 50.625 DEGREES
	DXY 12.,8	;HDG 0A : 56.250 DEGREES
	DXY 13.,7	;HDG 0B : 61.875 DEGREES
	DXY 14.,6	;HDG 0C : 67.500 DEGREES
	DXY 14.,4	;HDG 0D : 73.125 DEGREES
	DXY 15.,3	;HDG 0E : 78.750 DEGREES
	DXY 15.,1	;HDG 0F : 84.375 DEGREES
	DXY 15.,0	;HDG 10 : 90.000 DEGREES
;
	.SBTTL FLYING OBJECT POSITION UPDATE
;
;MOVE ACTIVE FLYING OBJECTS & TEST OFF-SCREEN
;IF OFF-SCREEN SET INTENSITY TO OFF AND FLAG AS AVAILABLE
;
FLYMUV:	LDX HITTER
	BNE 10$		;PLAYER USES OBJSTP FOR ORIENTATION
	LDA OBJSTP
	JMP 20$
10$:	LDA X,OBJHDG
20$:	JSR WIGGLE	;MOVE LEGS/JAWS ETC
;
;TEST FOR COLLISION WITH FENCES
;
MUTMUV:
NFLPLY:	LDX HITTER
	LDA X,OBJAUX	;DOES THIS KIND COLLIDE W/FENCES?
	AND #0F
	TAY
	LDA Y,OBJTYP
	AND #40
	BEQ GOWALL	;TEST FOR FENCE
MAKMUV:	JMP MVTEST	;THIS OBJECT CAN FLY OVER THEM
GOWALL:	LDA X,OBJFLG	;DYING OBJECTS NOT CHECKED
	AND #3
	BNE MOVBUG	;THIS ONE'S DYING
	LDA DIED	;FOLKS IGNORE WALLS WHEN FLEEING AS PLAYER DIES
	BNE MOVBUG
;
; THIS PROCESS COMPARES AN OBJECTS X/Y
; COORDINATES WITH KNOW SLOPES OF THE RADIAL
; WEB STRANDS AND DETERMINES WHICH
; STRANDS IT IS BETWEEN.
;
; ONCE THIS IS KNOWN, TABLE <WEBS> IS CONSULTED TO SEE
; IF A WALL EXISTS IN THIS SECTOR, IF SO; THEN THE SLOPE OF
; THE TENSION PIECE IS USED TO DETERMINE IF AN OBJECT
; HAS TOUCHED IT
;
; THE CURRENT OBJECT'S SUMMATION AS A FUNCTION OF X & Y
; IS LEFT IN TRIALY, TRIALX HOLDS INDEX INTO DIST2R
;
BUMPER:	LDX HITTER
	LDA WALKIN
	BEQ NOFRWL
MOVBUG:	JMP OK2MUV
;
NOFRWL:	JSR TRIABS	;MOVE ABS(ABSX&Y) TO TRIALX&Y
	JSR ARMS
	LDX HITTER
	BNE NOTUS	;NOT PLAYER
	LDA BRG2CN	;SAVE PLAYERS SECTOR# FOR WAVE
	STA SPDARM	;SO BADDIES DON'T GROW ON TOP
NOTUS:	LDY BRG2CN	;ARM NUMBER
;
; <WEBS> HAS 12 ENTRIES, 1 PER SECTOR
; EACH ENTRY HOLDS RING#*6 + COLOR BITS
; IN 2 MSB'S 00=NONE 01=GREEN 10=YELLOW 11=RED
;
	LDA Y,WEBS	;IS THERE A WALL THIS SECTOR?
	BEQ GOMOVE	;NOPE
	TAY		;SAVE COLOR & RING# IN Y
	AND #0C0	;0=NONE 1=GREEN 2=YELLOW 3=RED
	CMP #40		;GREEN?
	BEQ TWOWAY	;YEP, PLAYER UNAFFECTED
	JMP CHKWAY	;YELLOW OR RED
TWOWAY:	CPX #0		;DOING THE PLAYER?
	BNE CHKWAY	;YEP, NEVER HIT GREEN ANYWHERE
	JMP MVTEST
CHKWAY:	TYA		;GET COLOR BACK
	AND #3F		;DISCARD COLOR BITS FOR NOW
	CLC
	ADC TRIALX	;INDEX AS TO WHICH SUMMATION
	TAX
	LDA TRIALY	;LSB
	CMP X,DIST2R
	LDA TRIALY+1
	SBC X,DIST2R+1
	BEQ PING	;RIGHT ON TOP OF IT
	BPL TERR
	EOR #0FF
	CLC
	ADC #1
TERR:	CMP #4
	BCC PING
;
GOMOVE:	LDX HITTER
	JMP MVTEST
;
; WE'VE HIT A WALL, IF RED: EVERYBODY PONGS
; IF GREEN OR YELLOW; POINT INWARD
;
PING:	LDX HITTER
	LDA X,OBJAUX
	AND #0F
	CMP #6		;MUTANTS EAT ALL WALLS
	BNE GOCENT
	JMP BYEWAL	;EAT THE WALL
GOCENT:	TYA		;WHAT COLOR WALLS?
	AND #0C0
	CMP #0C0	;RED?
	BNE PCENTR	;NOPE POINT AT CENTER
	JMP PONG
PCENTR:	JSR SNDOB
	LDY BRG2CN
	LDA Y,CENTER
	LDX HITTER
	STA X,OBJHDG
	JSR FLYTRN
	JMP OK2MUV
;
; COMPUTE WHICH ARMS THE OBJECT IS BETWEEN
; UPON RETURN BRG2CN CONTAINS SECTOR NUMBER
; (I.E. ARM # THAT THIS OBJECT IS CLOCKWISE FROM)
; TRIAL Y&Y+1 THE SUM OF THE FUNCTION OF X&Y
; DESCRIBING THE SLOPE OF THE ARM IN THAT SECTOR
; AND TRIALX AN INDEX INTO DIST2R FOR COMPARISON
;
ARMS:	LDA TRIALX
	ASL		;IF MSB=1 X TOO BIG FOR ARM 11
	STA TEMP	;TEMP = 2*X
	LDA TRIALX+1
	ROL
	STA TEMP1
	LDA TRIALY
	CMP TEMP
	LDA TRIALY+1
	SBC TEMP1
	BCC KDST01	;Y<2*X
;
; Y>=2X SO MUST BE ARM 5 OR 11
;
	LDY #5
	LDA DY
	BMI KDST00	;YN: ARM 5
	LDY #11.
KDST00:	STY BRG2CN	;SAVE ARM NUMBER
	LDA #2	;USE Y FOR LIMIT TEST
	STA TRIALX	;Y ALREADY CONTAINS VALUE
	RTS
;
;
; Y<=2*X SO NOT ARMS 5 OR 11
; TEMP & TEMP1 = 2*X ALREADY
;
KDST01:	LDA TRIALX
	CLC
	ADC TEMP
	STA TEMP
	LDA TEMP1
	ADC TRIALX+1
	ROR
	ROR TEMP
	LSR
	ROR TEMP
	STA TEMP1
	LDA TEMP
	CMP TRIALY
	LDA TEMP1
	SBC TRIALY+1
	BCS KDST04	;Y<3/4X CAN'T BE 0,4,6,10
;
; MUST BE 0,4,6 OR 10
;
	LDY #0
	LDA DX	;XPYP:0 XNYP:10 XNYN:6 XPYN:4
	BPL KDST02	;0OR 4
	LDY #10.
	LDA DY
	BPL KDST03
	LDY #6
	JMP KDST03
KDST02:	LDA DY
	BPL KDST03
	LDY #4
KDST03:	STY BRG2CN
	LSR TRIALX+1	;Y+X/2
	ROR TRIALX	;MSB IN THESE TESTS
	CLC
	LDA TRIALX
	ADC TRIALY
	STA TRIALY
	LDA TRIALX+1
	ADC TRIALY+1
	STA TRIALY+1
	LDA #0
	STA TRIALX	;Y+X/2 FLAG
	RTS
;
; CAN'T BE 5,11,0,4,6,10
;
KDST04:	LDA TRIALY
	STA TEMP
	LDA TRIALY+1
	STA TEMP1
	ASL TEMP
	ROL TEMP1
	ASL TEMP
	ROL TEMP1
	BCS KDST06
	LDA TEMP
	ADC TRIALY
	STA TEMP
	LDA TEMP1
	ADC TRIALY+1
	STA TEMP1
	BCS KDST06
	LDA TRIALX
	CMP TEMP
	LDA TRIALX+1
	SBC TEMP1
	BCC KDST06
;
; MUST BE ARM 2 OR 8 SO PERFORM X LIMITS TEST
;
	LDY #2
	LDA DX
	BPL KDST05
	LDY #8
KDST05:	STY BRG2CN
	LDA TRIALX	;X LIMIT TEST
	STA TRIALY
	LDA TRIALX+1
	STA TRIALY+1
	LDA #0
	STA TRIALX
	RTS
;
; MUST BE 1,3,7 OR 9 TO GET HERE
;
KDST06:	LDY #1
	LDA DX
	BPL KDST07
	LDY #7
	LDA DY
	BMI KDST08
	LDY #9
	JMP KDST08
KDST07:	LDA DY
	BPL KDST08
	LDY #3
KDST08:	STY BRG2CN
	ASL TRIALX
	ROL TRIALX+1
	CLC
	LDA TRIALX
	ADC TRIALY
	STA TRIALY
	LDA TRIALX+1
	ADC TRIALY+1
	STA TRIALY+1
	BCS HUGHOO	;TOO BIG
BIGGIE:	LDA #4		;FLAG FOR Y+2X
	STA TRIALX
	RTS
HUGHOO:	LDA #0FF
	STA TRIALY	;SET AT MAX
	STA TRIALY+1
	JMP BIGGIE
;
; TAKE ABSOLUTE VALUE OF OBJECTS X&Y POSITION AND
; PLACE IT IN TRIALX&TRIALY
;
; DX AND DY HOLD THE SIGN OF THE ORIGINAL X&Y
;
TRIABS:	LDA X,OBJABX
	STA TRIALX
	LDA X,OBJABX+1
	STA TRIALX+1
	STA DX
	BPL XDLP
	EOR #0FF
	STA TRIALX+1
	LDA TRIALX
	EOR #0FF
	STA TRIALX
	INC TRIALX
	BNE XDLP
	INC TRIALX+1
XDLP:	LDA X,OBJABY
	STA TRIALY
	LDA X,OBJABY+1
	STA TRIALY+1
	STA DY
	BPL YDLP
	EOR #0FF
	STA TRIALY+1
	LDA TRIALY
	EOR #0FF
	STA TRIALY
	INC TRIALY
	BNE YDLP
	INC TRIALY+1
YDLP:	RTS
;
; PONG REFLECTION OFF RED WALLS
;
PONG:	LDY BRG2CN	;ARM (SECTOR) NUMBER
	LDX HITTER	;WHO'S DOING IT
;
; IF A MUTANT, MUNCH THE WALLS
;
	LDA X,OBJAUX
	AND #0F
	CMP #6
	BNE MIRROR
BYEWAL:	JSR SNDOE	;EATING SOUND
	LDX BRG2CN
	LDA X,WEBS	;GET RING# TO TURN OFF
	JSR PNTRNG	;WHERE IN VGLIST TO CHANGE COLOR
	LDA #<0A0!ZBLUE>
	LDY #0
	STA NY,TO
	LDY BRG2CN
	LDA #0		;SAY THERE'S NO WALL HERE
	STA Y,WEBS
	LDX HITTER
	JMP OK2MUV
MIRROR:	LDA Y,PONGER	;AZIMUTH*2 OF THIS WALL
	SEC
	SBC X,OBJHDG
	SEC
	SBC #1
	BPL POSHDG	;NO UNDERFLOW
	STA TEMP
	LDA #64.	;MODULO 64
	CLC
	ADC TEMP
POSHDG:	AND #3F
	STA X,OBJHDG	;NEW HEADING
;
; IF A MOSQUITO, SET GROUP HEADING
;
	LDA X,OBJAUX
	AND #0F
	BNE NOGRUP	;NOT A MOSQUITO
	LDA X,OBJHDG
	ORA #80
	STA MOSQUE
NOGRUP:	JSR FLYTRN
	JSR SNDOB
	LDX HITTER
MVTEST:	LDA X,OBJFLG	;NOT MOVING?
	AND #8
	BEQ TEDGE	;OKAY TO MOVE IT
	RTS
TEDGE:	CPX #0
	BEQ TRIPER	;KEEP PLAYER ON WEB
	LDA X,OBJAUX	;BEETLES TOO
	AND #0F
	CMP #7
	BNE OK2MUV
TRIPER:	LDA X,OBJABX+1
	STA TRIALX
	LDA X,OBJABY+1
	STA TRIALY
	LDA #7
	STA TRING
	JSR FITWEB
	LDX HITTER
	BCC OK2MUV
	LDA X,OBJHDG
	PHA
	LDY BRG2CN
	LDA Y,CENTER
	LDX HITTER
	STA X,OBJHDG
	JSR FLYTRN
	JSR ADELTA
	LDX HITTER
	PLA
	STA X,OBJHDG
	JSR SNDOB
	RTS
;
; IF THE PLAYER, NO OFF-SCREEN TEST...
; (FOR NOW)
;
OK2MUV:	JSR ADELTA
	LDA X,OBJFLG	;IF DYING THAT'S IT
	AND #DYING
	BEQ NOTYTD	;NOT DYING
	RTS
NOTYTD:	TXA
	BNE TEGGS
	JMP MOVXIT
;
TEGGS:	LDA X,OBJAUX	;TEST FOR EGGS
	AND #0F
	CMP #3
	BNE NTANS	;NOT A NEST
	LDA #7
	STA TRING
	LDA X,OBJABX+1
	STA TRIALX
	LDA X,OBJABY+1
	STA TRIALY
	JSR FITWEB
	BCC INHOLE
GOHOLE:	JMP SCRNST
;
; NEST IS WITHIN OUTER LIMIT
; TEST AGAINST FALLING INTO THE HOLE
;
INHOLE:	LDA #0
	STA TRING
	JSR FITMOR
	BCC GOHOLE
	RTS
;
NTANS:	LDA X,OBJABX+1	;OFFSCREEN TEST
	BPL BNCXP
	EOR #0FF
	CLC
	ADC #1
BNCXP:	CMP #70
	BCC MSLXOK	;WITHIN LIMIT
	LDY #6E		;SET AT MAX B4 PONG
	LDA X,OBJABX+1
	BPL XLMPOS
	LDY #92
XLMPOS:	TYA
	STA X,OBJABX+1
	LDA #0
	STA X,OBJABX
	STA TEMP	;FLAG FOR MSLYNG
	JMP MSLOFF
MSLXOK:	LDA X,OBJABY+1
	BPL BNCYP
	EOR #0FF
	CLC
	ADC #1
BNCYP:	CMP #60
	BCS MSLYNG
	RTS
KILWEB:	LDA #DEAD	;KILL BULLETS (NO WRAP)
	STA X,OBJFLG
MOVXIT:	RTS
;
MSLYNG:	LDY #5E		;SET AT LIMIT
	LDA X,OBJABY+1
	BPL YLMTPO
	LDY #0A2
YLMTPO:	TYA
	STA X,OBJABY+1
	LDA #0
	STA X,OBJABY
	LDA #20

	STA TEMP
MSLOFF:	LDA DIED	;IF PLAYER DYING EVERYONE DIES WHEN HITS BORDER
	BEQ WHOXIT	;PLAYER NOT DYING
	TXA
	TAY
	LDA X,OBJAUX	;MOST ARE RE-PLANTED
	AND #0F
	TAX
	CMP #4
	BEQ DIEBAD	;BULLETS AREN'T
	CMP #8		;NOR ARE TORPEDOES
	BEQ DIEBAD
	CMP #9		;NOR ARE T-BUGS
	BEQ DIEBAD
	CMP #0A		;NOR ARE OTHER T-BUGS
	BEQ DIEBAD
	CMP #6		;MUTANTS COME BACK AS HORNETS
	BNE INCBAD
	LDX #2		;MUTANT INDEX
INCBAD:	INC X,BADASS	;WE'LL SEE THIS ONE AGAIN
DIEBAD:	TYA
	TAX
	LDA #DEAD
	STA X,OBJFLG
	RTS
WHOXIT:	CPX #50	;BULLETS ARE 10,20,30,40
	BCS BEND
	JMP KILWEB
;
BEND:	LDA X,OBJAUX
	AND #0F
	TAY
	LDA Y,OBJTYP
	TAY
	AND #1		;NEVER BOUNCE?
	BNE KILWEB	;SOME OBJECTS NEVER BOUNCE BOUNDARY
	TYA		;STUNNED=EXIT FOR GOOD
	AND #8
	BEQ NSEQXT	;NOPE
	LDA X,OBJFLG
	AND #HEALTH
	BEQ NSEQXT
	LDA X,OBJAUX
	AND #0F
	CMP #09		;IF TBUGS... HOW MANY ARE LEAVING
	BNE KILWEB
	DEC NEGGS
	LDA NEGGS
	CMP #-11.	;LAST ONE?
	BNE KILWEB
	STX TEMP
	LDA #1		;10,000 POINTS
	STA TRIALX
	LDA #0
	STA TRIALX+1
	STA TRIALX+2
	JSR SCVIAT	;SPECIAL KINDA DEATH
	RTS
;
NSEQXT:	LDA TEMP	;ANGLE TO BOUNCE
	SEC
	SBC X,OBJHDG
	SEC
	SBC #1
	BPL BNCANP
	CLC
	ADC #64.
BNCANP:	AND #3F
	STA X,OBJHDG
	JSR FLYTRN
	JSR OK2MUV
	RTS
;
; ADD DELTA X TO ABX  AND DELTA Y TO ABY
; BY USING PRE-COMPUTED ADDRESS HELD IN
; OBJDX,OBJDY
;
ADELTA:	STX TEMP
	LDA X,OBJDX	;ADDRESS WITHIN MDLTAS
	STA FROM	;USE VIA IND(Y)
	LDA X,OBJDY	;MSB
	STA FROM+1
	LDX #3		;COPY 4 BYTES TO TEMP1...4
	LDY #3
GETDLT:	LDA NY,FROM
	STA X,TEMP1
	DEY
	DEX
	BPL GETDLT	;4 BYTES
	LDX TEMP
	LDA X,OBJHDG	;REFERENCE REFLECT BITS
	CMP #11		;1ST QUADRANT?
	BCS NOQ1	;NOPE
	JSR ADDDX	;ADD DX AND DY
ADDDY:	CLC
	LDA X,OBJABY
	ADC TEMP3
	STA X,OBJABY
	LDA X,OBJABY+1
	ADC TEMP4
	STA X,OBJABY+1
	RTS
NOQ1:	CMP #21		;IN 2ND QUADRANT?
	BCS NOQ2	;NOPE
	JSR SUBDY	;SUBTRACT DY
ADDDX:	CLC
	LDA X,OBJABX
	ADC TEMP1
	STA X,OBJABX
	LDA X,OBJABX+1
	ADC TEMP2
	STA X,OBJABX+1
	RTS
NOQ2:	CMP #31
	BCS NOQ3
	JSR SUBDX
SUBDY:	SEC
	LDA X,OBJABY
	SBC TEMP3
	STA X,OBJABY
	LDA X,OBJABY+1
	SBC TEMP4
	STA X,OBJABY+1
	RTS
NOQ3:	JSR ADDDY
SUBDX:	SEC
	LDA X,OBJABX
	SBC TEMP1
	STA X,OBJABX
	LDA X,OBJABX+1
	SBC TEMP2
	STA X,OBJABX+1
	RTS
;
SCRNST:	LDA #0
	STA TRIALX	;ZERO SCORE X????
	STA X,OBJAUX	;FALLING NESTS ARE LIKE MOSQUITOS (WHO CARES WHAT THEY WERE)
	STX TEMP	;SAVE OBJECT INDEX
	LDA #19.	;500*#EGGS
	ASL
	TAX
	LDA X,BIGPNT
	STA TRIALX+1
	LDA X,BIGPNT+1
	STA TRIALX+2
	TXA	;SAVE X
	PHA
	JSR SNDEG	;EGG SHOVED SOUND
	PLA
	TAX
	LDY NEGGS	;#EGGS PUSHED
	CPY #4		;(4+1)*(500)POINTS MAX
	BCS CONDEC	;GIVE 2500 MAXIMUM
	INC NEGGS	;INCREMENT # PUSHED
CONDEC:	SED
MULEGG:	DEY
	BMI SETSCO	;THAT'S IT
	LDA X,BIGPNT
	CLC
	ADC TRIALX+1
	STA TRIALX+1
	JMP MULEGG
SETSCO:	CLD
	LDA TRIALX+1
	TAY
	LDX TEMP
	LDA #60		;NORMAL STAT INSTRUCTION
	STA X,OBJVGC+1
	LDA #ZBLACK	;TURN OFF BODY/WINGS
	STA X,OBJVGC
	STA X,OBJVGC+6	;UNTIL FRAME ENDS
	LDA #71
	STA X,OBJVGC+11.
	LDA #0
	STA X,OBJVGC+10.	;REMOVE THE RING
	LDA TRIALX+2
	JMP DUESCO
;
; (X) HOLDS OBJECT INDEX
; (A) HOLDS POINT MULTIPLIER (A X 25 PNTS)
;
; MAKES NOISE, UPDATES SCORE, SETS DYING
;
ANTSCR:	STX TEMP
	PHA
	LDA WVTIME	;RESET 15 SECOND ALL-IN-USE FLAG
	STA WAVNUM	;HOLDS TIME OF LAST KILL
	LDA #60		;NORMAL STAT INSTRUCTION
	STA X,OBJVGC+1
	LDA #ZBLACK	;TURN OFF BODY/WINGS
	STA X,OBJVGC
	STA X,OBJVGC+6	;UNTIL FRAME ENDS
	PLA
	CMP #34.
	BNE NOBIGY
	BIT ATRACT	;CAN'T SCORE IN ATTRACT
	BMI 99$
	LDA RESPNT+2	;BIGTIME RESUME BONUS
	SED
	CLC
	ADC SCORE+2
	STA SCORE+2
	LDA RESPNT+1
	ADC SCORE+1
	STA SCORE+1
	LDA RESPNT
	ADC SCORE
	STA SCORE
	CLD
	LDA #0		;IN CASE THAT GAVE US A LIFE
	LDY #0
	JSR SCORIT
99$:	JMP BOMSCR
NOBIGY:	ASL		;2 BYTES/ENTRY
	TAX
	LDA #0
	STA TRIALX
	LDA X,BIGPNT
	TAY
	STA TRIALX+1
	LDA X,BIGPNT+1
	STA TRIALX+2
DUESCO:	JSR SCORIT
;
; NOW THAT SCORE HAS BEEN ADDED TO TOTAL
; DETERMINE IF OBJECT BLOWS UP OR SEE POINTS
;
	LDX TEMP
	LDA X,OBJAUX
	AND #0F
	TAY
	LDA Y,OBJTYP	;BOOM OR SCORE?
	BMI TBOMB	;IF BOOM & GRENADE STUN DON'T 'DYING'
SESCRE:	JSR SNDOF	;FALLING SOUND
	LDA #0		;SUPRESS LEADING 0'S
	STA TEMP2	;MINUS=NO SUPRESS
	LDA TEMP
	CLC		;SKIP THE BACKSPACE
	ADC #4
	STA VGLIST
	LDAH VGRAM
	CLC
	ADC #3
	STA VGLIST+1
	JSR DSCORE	;BUILD SCORE VGJSRL'S
	LDX TEMP
	TXA		;DETERMINE WHICH ENTRY TO USE
	LSR
	LSR
	LSR
	SEC
	SBC #10.
	TAY
	SEI
	LDA Y,SCLINK
	STA X,OBJVGC+4
	LDA Y,SCLINK+1
	STA X,OBJVGC+5
	LDA #0		;SCORE START OUT BIG
	STA X,OBJVGC+2
	LDA #70
	STA X,OBJVGC+3
	LDA #DYING
	STA X,OBJFLG
	CLI
	RTS
;
; THIS OBJECT IS SUPPOSED TO EXPLODE
;
TBOMB:	SEI
	CPY #0D		;IS THIS A GRENADE?
	BNE BOMSCR	;NOPE
	LDY #5		;500 POINTS ANYWAY
	LDA #0
	JSR SCORIT
	LDA #STUCK
	BNE SETBMB	;GRENADES DON'T REALLY 'DIE'
BOMSCR:	LDA #DYING
SETBMB:	STA X,OBJFLG
	LDA #0		;RING INDEX
	STA X,OBJRNG
	LDA #1
	STA X,OBJARM
	LDA SHWCIR	;SUBSTITUTE CIRCLE FOR BODY
	STA X,OBJVGC+4
	LDA SHWCIR+1
	STA X,OBJVGC+5
	CLI
	JSR SNDOX
	RTS
DSCORE:	LDA TRIALX
	AND #0F
	SEC
	BEQ SUPR
	CLC
	DEC TEMP2
SUPR:	JSR VGHEXZ	;FORCE MSB=0
	LDA TRIALX+1
	LSR		;DO 1000'S DIGIT
	LSR
	LSR
	LSR
	JSR DOHEX
	LDA TRIALX+1
	AND #0F
	JSR DOHEX
	LDA TRIALX+2
	LSR
	LSR
	LSR
	LSR
	CLC
	JSR VGHEXZ
	LDA TRIALX+2
	CLC
	JSR VGHEXZ
	RTS
DOHEX:	CLC	;NO SUPRESS
	BEQ TSGN	;IF EQUAL TEST PRIOR
	DEC TEMP2
	JMP VGHEXZ
TSGN:	BIT TEMP2	;IF PRIOR NON SUPR
	BMI GOHEX
	SEC
GOHEX:	JMP VGHEXZ
;
MOVEME:	LDX HITTER
	LDA VGX
	STA X,OBJABX
	LDA VGX+1
	STA X,OBJABX+1
	LDA VGY
	STA X,OBJABY
	LDA VGY+1
	STA X,OBJABY+1
	RTS
;
BIGPNT:	.BYTE 0,25,0,50,0,75,1,0
	.BYTE 1,25,1,50,1,75,2,0
	.BYTE 2,25,2,50,2,75,3,0
	.BYTE 3,25,3,50,3,75,4,0
	.BYTE 4,25,4,50,4,75,5,0
	.BYTE 5,25,5,50,5,75,6,0
	.BYTE 6,25,6,50,6,75,7,0
	.BYTE 7,25,7,50,7,75,8,0
	.BYTE 10,0
;
; PSEUDO MOTION-OBJECT HEADING TO
; DX/DY MOVEMENT ALLOWANCE CALCULATION
;
; USING HEADING AND SPEED, COMPUTE POINTER
; TO APPROPRIATE ENTRY IN MDLTAS
; STORE THIS ADDRESS IN OBJDX&OBJDY
; WHERE IT WILL BE LOOKED UP BY FLYMUV
;
FLYTRN:	CPX #50		;PLAYER & BULLETS ROUNDED TO NEAREST 45 IF NOT ATTRACT
	BCS NON45	;NOT THE PLAYER OR ONE OF THE BULLETS
	BIT ATRACT	;IN ATRACT?
	BMI NON45	;YEP, NON-45S ALLOWED
	LDA WALKIN	;WALKING-IN NOT LIMITED TO 45S ALSO
	BNE NON45	;WALKING ON-SCREEN
	LDA X,OBJHDG	;ROUND
	CLC
	ADC #4
	AND #38
	STA X,OBJHDG
NON45:	LDA X,OBJHDG
	STA TEMP3	;SAVE THE WORKING HEADING
	CMP #11		;OUT OF 1ST QUADRANT?
	BCC FNDLTA	;NOPE, IN 1ST QUADRANT
	CMP #21		;OUT OF 2ND QUADRANT
	BCS NTQD2	;YEP, TRY 3RD
	LDA #20
	SEC
	SBC TEMP3
	STA TEMP3
	JMP FNDLTA
NTQD2:	CMP #31		;OUT OF 3RD QUADRANT?
	BCS NTQD3	;YEP, MUST BE IN 4TH
	SEC
	SBC #20
	STA TEMP3
	JMP FNDLTA
NTQD3:	LDA #40
	SBC TEMP3
	STA TEMP3
FNDLTA:	LDA X,OBJSPD	;USE SPEED AS MULTIPLIER INDEX
	ASL
	ASL
	ASL
	ASL
	STA TEMP2
	LDA TEMP3	;GET THE HEADING INDEX
	AND #1F
	LSR
	ROR TEMP2
	LSR
	ROR TEMP2
	STA TEMP1
	LDAL MDLTAS	;ADDRESS LSB
	CLC
	ADC TEMP2	;+SPEED & HEADING INDEX
	STA X,OBJDX	;SAVE TABLE LSB
	LDAH MDLTAS
	ADC TEMP1
	STA X,OBJDY	;MSB OF TABLE ADDRESS
	RTS
;
	.END
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                          