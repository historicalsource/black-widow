	.TITLE BBTEXT
	.INCLUDE BBRAM
	.INCLUDE HLL65F
;
;      *******************************************************
;      * COPYRIGHT 1982 ATARI. UNAUTHORIZED REPRODUCTION,    *
;      * ADAPTATION, DISTRIBUTION, PERFORMANCE OR DISPLAY OF *
;      * THIS COMPUTER PROGRAM OR THE ASSOCIATED AUDIOVISUAL *
;      * WORK IS STRICTLY PROHIBITED!!!!!!!                  *
;      *******************************************************
;
; EXTERNAL REFERENCES
;
	.GLOBL MHELP1,MHELP2,MHELP3,MHELP4
	.GLOBL VGSCA1,VGSTAT,VGCNTR,VGVTR1,VGADD,VGRTSL
	.GLOBL VGHEXZ,SEEPNT,VGSCAL
	.GLOBL MSGLBS,VGMSGA,CCREDT
	.GLOBL DLR0,WNG00,VGJSRL,VGSCA1,VGCNTR
	.GLOBL MPLAYR,MPLYR2,MNXTBO
	.GLOBL MRATE1,MRATE2,FACES,SPBODY
	.GLOBL TTSTAT,TTSCL2,SHOW1
;
; MESSAGE NAMES
;
	.GLOBL MGAMOV,MPRES1,MLEVEL
	.GLOBL MINSER,MCMOD2,MATARI,MCREDI
	.GLOBL MSURV1,MSURV2,MTYME,HALFER
;
; THIS ROUTINE HANDLES ALL TEXT MESSAGES
; (EXCEPT SCORES DURING PLAY) THAT ARE
; SEEN ON THE DISPLAY.  IT IS CALLED
; AT THE START OF EACH FRAME WHILE THE WEB
; IS BEING DRAWN, AND THUS OPERATES WHILE
; THE VG IS RUNNING
;
; MESSAGES ARE THE LAST THING DRAWN IN THE
; FRAME AND BEGIN AT 2400 HEX (128 CHAR MAX)
;
TEXT::	LDA MSGADR	;RESET VGLIST TO START OF BUFR
	STA VGLIST
	LDA MSGADR+1
	STA VGLIST+1
;
; HANDLE FIELD TEST COIN MODES
;
	BIT ATRACT	;IN ATRACT
	BPL NXTBON	;NOPE
;
; SHOW HIGH SCORE TO DATE IN ATTRACT GAME PLAY
;
;
; IF IN GAME PLAY (DSTATE =1) THEN SHOW PROMPTS
;
	LDA DSTATE
	CMP #1
	BNE 93$	;NOT HELP SCREEN
	LDA BUGSHW	;GET CURRENT DISPLAY
	CMP #8
	BCS 93$
	LSR
	TAX
	LDA X,HELPME
	TAX
	JSR MESG
93$:	LDA #0EC	;X POSITION
	STA TRIALX
	LDA #060	;Y POSITION
	STA TRIALY
	LDA TTSTAT	;COLOR
	STA FOODX
	LDA #40		;SMALLER THAN LIFE-SIZE
	STA TRIALX+1
	LDA TTSCL2	;BINARY SCALE
	STA TRIALY+1
	LDX #0		;HIGH SCORE INDEX
	STX TEMP
	LDY #0
	JSR SHOW1	;PUT INITIALS & SCORE IN LIST
	DEY
	JSR VGADD
	JMP BEGGER
;
; SEE NEXT BONUS SCORE
;
NXTBON:	LDA NPLYRS	;2 PLAYERS?
	BEQ 13$		;NOPE
	LDA PLAYER
	BEQ 13$
	LDA LIVES
	STA GSAVE2+LIFE
	JMP 14$
13$:	LDA LIVES
	STA GSAVE1+LIFE
14$:	LDA BLIVES	;SET ON NO BONUS?
	BEQ PLYMSG	;DON'T TELL HIM
	LDX #MNXTBO
	JSR MESG
	LDA PRVMSB
	STA TEMP4	;BONUS INTERVAL COUNT
	LDA #0
	SED
49$:	CLC
	ADC BLIVES
	DEC TEMP4
	BPL 49$
	CLD
	STA TEMP1
	LSR
	LSR
	LSR
	LSR
	BEQ 22$
	JSR VGHEXZ
22$:	LDA TEMP1
	JSR VGHEXZ
	LDA #0
	JSR VGHEXZ
	LDA #0
	JSR VGHEXZ
	LDA #0
	JSR VGHEXZ
	LDA #0
	JSR VGHEXZ
PLYMSG:	JMP NOSTRT
BEGGER:	JSR CCREDT	;SEE # CREDITS
	LDX #MATARI
	JSR MESG
	LDA $CMODE	;IF FREE PLAY SAY THERE ARE 2 COINS ALWAYS
	AND #3
	BEQ PUSHIT	;FREE PLAY
	LDA $$CRDT	;COINS AVAILABLE?
	BNE PUSHIT	;YEP, SAY PRESS START
	LDX #MINSER
	JSR MESG	;INSERT COINS
	JMP NOSTRT	;PAST START MESSAGE
PUSHIT:	LDX #MPRES1	;PRESS START
	JSR MESG
NOSTRT:	BIT ATRACT	;IN ATRACT?
	BMI NOWAVE	;NO SEE LEVEL#
	LDX #MLEVEL	;SEE "LEVEL "
	JSR MESG
	LDA BINWAV
	CLC
	ADC #1
	JSR HEXBCD
	PHA
	LSR
	LSR
	LSR
	LSR
	SEC
	JSR VGHEXZ
	PLA
	AND #0F
	JSR VGHEXZ
NOWAVE:	JSR VGRTSL	;PUT IN A RETURN
	JSR SEEPNT	;UPDATE SCORE DISPLAYS
	RTS
;
HELPME:	.BYTE MHELP1,MHELP2,MHELP3,MHELP4
;
;
DOHEX:	CLC	;NO SUPRESS
	BEQ TSGN	;IF EQUAL TEST PRIOR
	DEC TEMP2
	JMP VGHEXZ
TSGN:	BIT TEMP2	;IF PRIOR NON SUPR
	BMI GOHEX
	SEC
GOHEX:	JMP VGHEXZ
;
; TEXT PROCESSES FOR SELECT STARTING WAVE SCREEN
;
TEXTS::	LDA MSGADR	;RESET VGLIST TO START OF BUFR
	STA VGLIST
	LDA MSGADR+1
	STA VGLIST+1
;
; SHOW THE SPIDER WITH FACE MODULO 8 FOR NOW
;
	LDA #0		;X COORD OF SPIDER PICTURE
	LDX #28		;Y COORD
	JSR VGVTR1
	LDA #0
	JSR VGSCA1	;DOUBLE SIZE FOR SPIDER
	LDA BINWAV
	AND #078
	LSR
	LSR
	CMP #0A
	BCC 13$
	LDA #8
13$:	TAX
	LDY #0
	LDA X,FACES
	STA NY,VGLIST
	INY
	LDA X,FACES+1
	STA NY,VGLIST
	INC VGLIST
	INC VGLIST
	JSR VGCNTR
	LDA #1
	JSR VGSCA1
	LDA #0		;X COORD OF SPIDER PICTURE
	LDX #28		;Y COORD
	JSR VGVTR1
	LDA #0
	LDY #80!ZRED
	JSR VGSTAT
	LDA #0
	JSR VGSCA1
	LDY #0
	LDA SPBODY
	STA NY,VGLIST
	INY
	LDA SPBODY+1
	STA NY,VGLIST
	INC VGLIST
	INC VGLIST
	LDA #1
	JSR VGSCA1
	LDA NPLYRS
	BEQ SNGPLY	;SINGLE PLAYER
	LDX #MPLAYR	;SHOW PLAYER NUMBER
	JSR MESG
	LDA PLAYER
	CLC
	ADC #1
	JSR VGHEXZ
SNGPLY:	LDX #MTYME
	JSR MESG
	LDA DTIME
	JSR HEXBCD
	PHA
	LSR
	LSR
	LSR
	LSR
	SEC
	JSR VGHEXZ
	PLA
	AND #0F
	CLC
	JSR VGHEXZ
	LDX #MRATE1	;"LEFT STICK SELECT STARTING WAVE"
	JSR MESG
	LDX #MRATE2	;"PRESS START TO BEGIN GAME"
	JSR MESG
	LDX #MSURV1	;"SHOOT  $  TO COLLECT BONUS"
	JSR MESG
	LDA #090
	LDX #2
	JSR SHWBUG
	LDX #MSURV2
	JSR MESG
;
; SHOW 3 POSSIBLE STARTING POINTS
;
	LDA #3
	STA TEMP4
	LDA BINWAV
	SEC
	SBC #4
	STA TEMP3
	LDA #0C8
	STA TEMP2
;
SHOW3:	LDA TEMP3	;VALID ENTRY?
	BPL 6$
	JMP NOBONE
6$:	JSR VGCNTR
	LDA #0BB
	LDX TEMP2	;Y COORD OF THIS LINE
	JSR VGVTR1
;
	LDY #0C0!ZBLUE
	LDA TEMP4
	CMP #2
	BNE 55$	;NOT MIDDLE ENTRY
	LDA FRMCNT
	LSR
	LSR
	AND #7
	BNE 44$
	LDA #7
44$:	ORA #0E0
	TAY
55$:	LDA #0
	JSR VGSTAT
	LDA TEMP3
	CLC
	ADC #1
	JSR HEXBCD
	PHA
	LSR
	LSR
	LSR
	LSR
	SEC
	JSR VGHEXZ
	PLA
	AND #0F
	CLC
	JSR VGHEXZ
;
; CLEAR RESPNT
;
	LDA #0
	STA RESPNT
	STA RESPNT+1
	STA RESPNT+2
;
	LDA #10
	LDX #0
	JSR VGVTR1	;SPACE TO RIGHT FOR POINTS
;
; HOW MANY POINTS THE $ IS WORTH
;
;
; 15,000 MINIMUM PER WAVE RESTART (EVERY 4TH WAVE)
;
	LDA TEMP3	;VARYING STARTING WAVE NUMBER
	BEQ 33$
	LSR
	LSR
	SEC
	SBC #1
	STA TEMP1
	ASL
	ADC TEMP1	;WAVE*3 PER ENTRY
	TAX
	LDA X,RSCORE
	STA RESPNT
	LDA X,RSCORE+1
	STA RESPNT+1
	LDA X,RSCORE+2
	STA RESPNT+2
33$:	LDA BINWAV
	CMP TEMP3
	BNE 14$
	LDA RESPNT+2
	PHA
	LDA RESPNT+1
	PHA
	LDA RESPNT
	PHA
14$:	LDA RESPNT
	LSR
	LSR
	LSR
	LSR
	SEC
	BEQ SUPR
	CLC
SUPR:	JSR VGHEXZ	;FORCE MSB=0
	LDA RESPNT
	JSR DOHEX
	LDA RESPNT+1
	LSR		;DO 1000'S DIGIT
	LSR
	LSR
	LSR
	JSR DOHEX
	LDA RESPNT+1
	AND #0F
	JSR DOHEX
	LDA RESPNT+2
	LSR
	LSR
	LSR
	LSR
	CLC
	JSR VGHEXZ
	LDA RESPNT+2
	CLC
	JSR VGHEXZ
;
; SEE APPROPRIATE COMMENT
;
	JSR VGCNTR
	LDA #0C
	LDX TEMP2
	JSR VGVTR1
	LDA TEMP3
	AND #78
	LSR
	LSR
	CMP #24.
	BCC 32$
	LDA #22.
32$:	TAX
	LDA X,COMENT
	STA FROM
	LDA X,COMENT+1
	STA FROM+1
	LDA #0FF
	STA TEMP1
88$:	INC TEMP1
	LDY TEMP1
	LDA NY,FROM
	LDY #0
	PHA
	AND #7F
	TAX
	LDA X,VGMSGA
	STA NY,VGLIST
	INY
	LDA X,VGMSGA+1
	STA NY,VGLIST
	LDA VGLIST
	CLC
	ADC #2
	STA VGLIST
	BCC 35$
	INC VGLIST+1
35$:	PLA
	BPL 88$
NOBONE:	LDA TEMP3
	BMI 10$
	CMP MAXRES
	BCS SEEALL
10$:	CLC
	ADC #4
	STA TEMP3
	LDA TEMP2
	SEC
	SBC #0C
	STA TEMP2
	DEC TEMP4
	BEQ SEEALL
	JMP SHOW3
SEEALL:	PLA
	STA RESPNT
	PLA
	STA RESPNT+1
	PLA
	STA RESPNT+2
	JSR VGRTSL
	RTS
;
SHWBUG:	JSR VGVTR1
	LDY #<0C0!ZGREEN>
	LDA #0
	JSR VGSTAT
	LDA #1
	TAY
	JSR VGSCA1
	LDAL DLR0
	TAX
	LDAH DLR0
	JSR VGJSRL
	LDY #<80!ZWHITE>
	LDA #0
;WHITE WINGS
	JSR VGSTAT
	LDAL WNG00
	TAX
	LDAH WNG00
	JSR VGJSRL
	LDY #0
	LDA #0
	JSR VGSTAT
	LDA #1
	JSR VGSCA1
	LDA #10
	LDX #-2
	LDY #0		;SPACE OVER TO RIGHT FOR CONTINUED TEXT
	JSR VGVTR1
	RTS
	.PAGE
	.SBTTL HEX TO BCD CONVERSION
;
; BINARY IN (A) TO BCD IN (A)
;
HEXBCD::	SED
	STA TEMP
	LDA #0
	STA TEMP1
	LDY #7
HEXLUP:	ASL TEMP
	LDA TEMP1
	ADC TEMP1
	STA TEMP1
	DEY
	BPL HEXLUP
	CLD
	RTS
	.PAGE

	.SBTTL MESSAGE ROUTINE
;
;BLOCK NAME:	MESG
;DESCRIPTION:	WILL OUTPUT SPECIFIED MESSAGE TO SPECIFIED LOCATION ON SCREEN
;INPUT PARAMS:	(X)=MESSAGE # * 2
;OUTPUT PARAMS:	NONE
;REGISTERS:	A,X,Y
MESG::
	LDA AX,MSGLBS+1
MSGEN3:	STX SAVEX
	STA TEMP2
	LDY SAVEX
	LDA NY,LITRAL		;GET LITERAL PTR
	STA INDYLO
	INY
	LDA NY,LITRAL
	STA INDYHI
	LDY I,0
	LDA NY,INDYLO		;GET HORIZ POSITION FROM LITERAL
	STA TEMP1
MSGENT: JSR VGCNTR
MSGEN2:	LDA I,0
	STA VGBRIT
	LDA I,1
	JSR VGSCA1
	LDA TEMP1
	LDX TEMP2
	JSR VGVTR1		;C  POSITION BEAM (USE VGBRIT)
MSGNOP:
	LDY SAVEX		;SET UP PTR TO LITERAL
	LDA NY,LITRAL
	STA INDYLO
	INY
	LDA NY,LITRAL
	STA INDYHI
	LDX SAVEX
	LDA X,MSGLBS
	PHA
	LSR
	LSR
	LSR
	LSR
	ORA #0C0
	TAY
;
; IF BIT 3 SET IN COLOR THEN IT FLASHES
;
	AND #8
	BEQ 10$
	LDA ONEHZ
	AND #0C0
	BNE 10$
	LDY #0
10$:	LDA #0
	JSR VGSTAT		;SET COLOR
	PLA
	LDY #10		;FOR SCALE 71
	AND I,0F
	CMP #1
	BEQ 20$
	LDY #68
	LDA #1
20$:	JSR VGSCAL		;SET SCALE
	LDY I,1
	LDA I,0			;C  INIT VGLIST OFFSET
	STA TEMP1
	BEGIN
	LDA NY,INDYLO		;C  GET CHARACTER REPRESENTATION
	STA TEMP2
	AND I,7F
	INY
	STY TEMP3		;	SAVE Y
	TAX
	LDA AX,VGMSGA		;C  GET CORRECT JSRL
	LDY TEMP1
	STA NY,VGLIST
	INY
	LDA AX,VGMSGA+1
	STA NY,VGLIST
	INY
	STY TEMP1		;	SAVE Y
	LDY TEMP3		;C  GET CHARACTER PTR
	BIT TEMP2		;D  IF NOT END OF STRING
	MIEND
	LDY TEMP1		;C  UPDATE VGLIST
	DEY
	JMP VGADD
;NO VERTICAL POSITIONING (Y)
MSGFOL: STX SAVEX		;INPUT: X=MSG#*2
	STA TEMP1		;       ACC=X OFFSET
	LDA I,0
	STA TEMP2
	JMP MSGEN2		;ALWAYS
;
;
;
	.SBTTL	.ASCVG - VG MODIFIED 'ASCII'

;	PROGRAMMER: RICH MOORE

;
;MACRO .ASCVG CONVERTS STRING OF ALPHANUMERICS TO OFFSET VALUES INTO
;VGMSGA TABLE (TABLE OF VG CHARACTER JSRL'S)
;
	.MACRO .ASCVG .STRING
	.NCHR ..N,<.STRING>
	.IRPC ..C,<.STRING>
	.IF EQ,''..C-^H20
	..X=0			;BLANK CHAR=0
	.IFF
	.IF GT,''..C-^H40
	..X=''..C-^H36		;ALPHA'S ^B TO ^25
	.IFF
	..X=''..C-^H2F		;NUMBERS 1 TO ^A
	.ENDC
	.ENDC
	..X=..X*2		;..X=VGMSGA TABLE OFFSET
	..N=..N-1
	.IIF EQ,..N,..X=..X!^H80	;SET LAST CHAR OF MSG FLAG
	.BYTE ..X
	.ENDR
	.ENDM
	.LIST	MEB
	.NLIST	BYT
;
COMENT:	.WORD CMNT1
	.WORD CMNT2
	.WORD CMNT3
	.WORD CMNT4
	.WORD CMNT5
	.WORD CMNT6
	.WORD CMNT7
	.WORD CMNT8
	.WORD CMNT9
	.WORD CMNT10
	.WORD CMNT11
	.WORD CMNT12
;
CMNT1:	.ASCVG <   FER SURE    >
CMNT2:	.ASCVG <   FAR OUT     >
CMNT3:	.ASCVG <   INTENSE     >
CMNT4:	.ASCVG <    UNREAL     >
CMNT5:	.ASCVG <   AWESOME     >
CMNT6:	.ASCVG <    INSANE     >
CMNT7:	.ASCVG <   TUBULAR     >
CMNT8:	.ASCVG <TOTALLY AWESOME>
CMNT9:	.ASCVG <LUNATIC FRINGE >
CMNT10:	.ASCVG <  TO THE MAX   >
CMNT11:	.ASCVG <    COSMIC     >
CMNT12:	.ASCVG <    NO WAY     >
;
; THIS FIXED TABLE REPLACES PSCORE
; WITH FIXED VALUES FOR RESTARTING THE GAME
;
	.RADIX 16
;
; 1...25 ARE 25,000 PER SET OF 4 WAVES
;
RSCORE:	.BYTE 02,50,00,05,00,00,07,50,00,10,00,00	;1,5,9,13
	.BYTE 12,50,00,15,00,00,17,50,00		;17,21,25
;
; 29...97 ARE 40,000 PER SET OF 4
;
	.BYTE 21,50,00,25,50,00,29,50,00,33,50,00	;29,33,37,41
	.BYTE 37,50,00,41,50,00,45,50,00,49,50,00	;45,49,53,57
	.BYTE 53,50,00,57,50,00,61,50,00,65,50,00	;61,65,69,73
	.BYTE 69,50,00,73,50,00,77,50,00,81,50,00	;77,81,85,89
	.BYTE 85,50,00,89,50,00				;93,97
;
CCREDT:	LDX #MCREDI
	JSR MESG
	LDA $CMODE
	AND #3
	BNE 27$		;SHOW REAL CREDITS
	LDA #2		;PHONEY CREDITS
	BNE 35$
27$:	LDA $$CRDT	;#COINS ALIVE
35$:	JSR HEXBCD
	PHA
	LSR
	LSR
	LSR
	LSR
	SEC
	JSR VGHEXZ
	PLA
	AND #0F
	CLC
	JSR VGHEXZ
	LDA $CMODE	;IF NOT 2 COINS/1 PLAY THEN SKIP
	AND #3
	EOR #3
	BNE WEAD
	LDA $CNCT	;FRACTIONAL COINS?
	BEQ WEAD
	LDAL HALFER
	TAX
	LDAH HALFER
	JSR VGJSRL
WEAD:	RTS
	.END
                                                                                                                                  