	.TITLE BBHITS
	.INCLUDE BBRAM
;
;      *******************************************************
;      * COPYRIGHT 1982 ATARI. UNAUTHORIZED REPRODUCTION,    *
;      * ADAPTATION, DISTRIBUTION, PERFORMANCE OR DISPLAY OF *
;      * THIS COMPUTER PROGRAM OR THE ASSOCIATED AUDIOVISUAL *
;      * WORK IS STRICTLY PROHIBITED!!!!!!!                  *
;      *******************************************************
;
; EXTERNAL REFERENCES
;
	.GLOBL SCVIAT	;SCORE LAST TBUG LEAVING
	.GLOBL SESCRE	;FALLING SCORE
	.GLOBL SHWCIR	;VG LOG CIRCLES
	.GLOBL FLYTRN
	.GLOBL DIST2R	;WEB COLLISIONS
	.GLOBL FLYMUV	;FOR MUTANT SWATTING
	.GLOBL MUTMUV	;FOR PUSHING & THUMPING
	.GLOBL ANTSCR	;KILL & SEE POINTS
	.GLOBL ATAN	;ARC-TANGENT ROUTINE
	.GLOBL SCORIT	;SCORE UPDATE
	.GLOBL SNDPP	;PLAYER DIES SOUND
	.GLOBL INISOU	;INITIALIZE SOUNDS
	.GLOBL SNDOF	;SOUND OF FALLING OBJECT
	.GLOBL SNDHE	;SOUND OF HORNET LAYING AN EGG
	.GLOBL SNDOO	;OH-OH FFF LAUNCHES A TORPEDO
	.GLOBL SNDPH	;SOUND OF BULLET HITTING TARGET
	.GLOBL SNDOE	;SOUND OF OBJECT BEING EATEN
	.GLOBL ZRED	;VG COLOR CODE
	.GLOBL WHOSIZ	;OBJECT SIZE TABLE
;
; GLOBAL DEFINITIONS
;
	.GLOBL FITWEB
	.GLOBL CENTER
	.GLOBL FITMOR
	.GLOBL SMASH
	.GLOBL SMELL
	.GLOBL CHEAT
	.GLOBL MAKEIT
;
; X = HITTER BASE
; Y = HITTEE BASE
;
; COMPUTE DISTANCE BETWEEN OBJECTS
; AND IF DISTANCE IS MINIMUM; COMPUTE
; DIRECTION FROM HITTER TO HITTEE
;
; CLOSEST TARGET IS SAVED IN <MINTGT>
; DISTANCE IN <MINRNG>
; HEADING IN <MINHDG>
;
; DISTANCE APPROX IS LARGER + 3/8 SMALLER
;
SMELL:	LDA X,OBJABX+1	;GET HITTEE
	CMP #80	;SIGN EXTEND & /2
	ROR
	STA TARGEX
	LDA Y,OBJABX+1
	CMP #80
	ROR
	SEC
	SBC TARGEX
	STA DX
	BPL DXPOS
	EOR #0FF
	CLC
	ADC #1
DXPOS:	STA DX+1	;DX * 2 FOR RANGE CALC
	LSR 
	STA TARGEX
;
; NOW DO Y'S
;
	LDA X,OBJABY+1	;GET HITTER
	CMP #80	;SIGN EXTEND & /2
	ROR
	STA TARGEY
	LDA Y,OBJABY+1
	CMP #80
	ROR
	SEC
	SBC TARGEY
	STA DY
	BPL DYPOS
	EOR #0FF
	CLC
	ADC #1
DYPOS:	STA DY+1	;DY * 2 FOR RANGE CALC
	LSR 
	STA TARGEY
;
	CMP TARGEX	;WHICH IS BIGGER
	BCC DXGTR	;DX IS LARGER
;
	LDA TARGEX	;RANGE=DY+3/8DX
	CLC
	ADC DX+1
	LSR
	LSR
	LSR
	CLC
	ADC TARGEY
	JMP RNGCHK	;TEST FOR MINIMUM
;
DXGTR:	LDA TARGEY
	CLC		;RANGE=DX+3/8DY
	ADC DY+1
	LSR
	LSR
	LSR
	CLC
	ADC TARGEX
;
RNGCHK:	CMP MINRNG	;NEW MINIMUM?
	BCS NOTMIN	;NOPE
	BIT STING	;ARE WE TESTING KILL RADIUS?
	BPL NOOSTG	;NOPE
	TYA		;ANTSCR NEEDS OBJECT# IN X
	TAX
	BNE ALLDIE	;DON'T EXPLODE THE PLAYER
;
; PLAYER HAS BEEN TAGGED BY A GRENADE RING
; IF IN DEMONSTRATION MODE IGNORE IT...
;
	LDA $TOUGH
	CMP #3
	BEQ HAHAHA	;MISSED ME...
	LDA TEMP1	;GRENADE INDEX
	STA DIED	;LET MAIN DETECT THIS
	JSR INISOU	;STOP SOUNDS
	JSR SNDPP	;PLAYER DIES SOUND
HAHAHA:	RTS
ALLDIE:	LDA X,OBJAUX	;VCB AND FFF RETURN TO HAUNT
	AND #0F
	CMP #0E
	BCC NOTAGN	;NOPE
	TAY
	LDA #1
	STA Y,BADASS
NOTAGN:	LDA X,OBJAUX	;BONUS $ BUG SCORES BIG
	AND #0F
	CMP #5
	BNE NOBOUN
	LDA #1
	STA X,OBJDLY	;SO TRIGGERED GRENADES START QUICK
	LDA #34.	;BONUS VALUE
	JMP ANTSCR
NOBOUN:	LDA #13		;500 POINTS PER BODY
	JMP ANTSCR	;WHEN GRENADE EXPLODES
NOOSTG:	STY MINTGT	;SAVE WHO IT IS
	STA MINRNG
	LDX DY		;COMPUTE HEADING
	LDY DX
	JSR ATAN
	AND #3F
	STA MINHDG
NOTMIN:	RTS
	.SBTTL <SMASH> COLLISION DETECTION PROCESS
;
; SMASH : COLLISION ROUTINE FOR ALL CASES
;
; INPUT:        Y = BASE OFFSET OF "HITTEE"
;
; OUTPUT: <HITS> HOLDS NUMBER OF COLLISIONS
;         <HITS+1...30> HOLDS OBJECT NUMBERS
; FOLLOWED BY OBJECT TYPE
;
SMASH:	LDX HITTER
	LDA #0		;PRESET COLLISIONS TO 0
	STA HITS
	LDA X,OBJFLG	;IS HITTER DEAD?
	AND #DYING	;DEAD OR DYING?
	BEQ SMASH$	;NOPE
	RTS
;
LNKMIS:	JMP MISSED
;
SMASH$:	LDA Y,OBJFLG	;IS HITTEE DEAD OR DYING?
	AND #DYING
	BNE LNKMIS	;YEP, DO NEXT
;
	CPY HITTER	;CAN'T HIT OURSELVES
	BEQ LNKMIS
CHEAT:	LDX HITTER	;HITTER BASE OFFSET
	SEC		;SUBTRACT ABSOLUTE X'S
	LDA X,OBJABX+1
	SBC Y,OBJABX+1
	STA DX		;DELTA X LSB
	BPL DXOK	;DELTA POSITIVE
	EOR #0FF
	STA DX
	INC DX
;
DXOK:	SEC
	LDA X,OBJABY+1
	SBC Y,OBJABY+1
	STA DY
	BPL DYOK
	EOR #0FF
	STA DY
	INC DY
DYOK:	CLC
	LDA Y,OBJSIZ
	ADC X,OBJSIZ
	CMP DX
	BCC MISSED
	CMP DY
	BCC MISSED
	STA MINRNG
	ASL
	ADC MINRNG
	LSR
	STA MINRNG
	LDA DX
	CLC
	ADC DY
	BCS MISSED
	CMP MINRNG
	BCS MISSED
;
	LDX HITS	;GET CURRENT INDEX
	INX		;ADD 1 FOR OBJECT BASE
	TYA
	STA X,HITS	;STORE OBJ BASE
	INX
	LDA Y,OBJAUX	;STORE OBJ TYPE
	AND #0F
	STA X,HITS
	STX HITS
;
MISSED:	TYA		;INCREMENT FOR NEXT OBJECT
	CLC
	ADC #10
	TAY
	BCS SMSXIT
	JMP SMASH$
SMSXIT:	RTS
	.SBTTL <DOHITS> COLLISION PROCESSING LINKAGE
;
; COLLISION PROCESSING LINKAGE
;
; <HITS> HOLDS # OF COLLISIONS
; <HITTER> HOLDS BASE ADDR OF HITTER
; <HITS+1...+30> HOLDS BASE ADDR OF HITTEE
; FOLLOWED BY OBJECT TYPE
;
MAKEIT:	LDA HITTER
	STA WRKOBJ
	JMP DOHITS-1
;
NXTHIT:	DEC HITS	;ALL PROCESSING ROUTINES RETURN HERE
	DEC HITS
;
	LDA WRKOBJ
	STA HITTER
	TAY
;
DOHITS:	LDX HITS	;ANY COLLISIONS LEFT TO PROCESS?
	BEQ HITXIT	;NOPE
	LDA X,HITS	;GET HITTEE TYPE
	STA TEMP
	LDA Y,OBJFLG	;JUST IN CASE HE DIED
	AND #DYING
	BNE NXTHIT	;DEAD OR DYING
	LDA Y,OBJAUX
	AND #0F
	STA TEMP1	;AT FINISH TEMP1>TEMP
	CMP TEMP	;HITTER MUST BE SMALLER
	BNE NOTSAM	;TYPES ARE DIFFERENT
	STY HITTER	;IF OBJECTS SAME HITTER/HITTEE IS ARBITRARY
	TAY		;OBJECT TYPE
	DEX
	LDA X,HITS	;INDEX TO HITTEE
	STA HITTEE
	TYA
	ASL
	TAX
	LDA X,SAMHIT+1	;TABLE FOR LIKE OBJECT COLLISIONS
	PHA
	LDA X,SAMHIT
	PHA
	LDY HITTEE
	LDX HITTER
	RTS
;
; OBJECTS COLLIDING ARE NOT OF SAME TYPE
;
NOTSAM:	BCS HTRBGR	;HITTER IS THE LARGER
	STA TEMP	;HITTER TYPE
	LDA X,HITS	;HITTEE TYPE
	STA TEMP1
	DEX		;GET HITTEE BASE
	LDA X,HITS
	STA HITTEE
	JMP MAKINX
HTRBGR:	STY HITTEE
	DEX
	LDA X,HITS
	STA HITTER
MAKINX:	DEC TEMP1
	LDA TEMP1
	SEC
	SBC TEMP
	STA TEMP1
	LDA TEMP	;GET SMALLER TYPE
	TAX
	LDA X,HINDEX	;OFFSET TO START OF HITTER COLLISIONS
	ASL TEMP1	;LARGER TYPE OFFSET
	ADC TEMP1
	TAX
;
	LDA X,HITABL+1	;GET COLLISION PROCESSOR ADDR
	PHA
	LDA X,HITABL
	PHA
	LDY HITTEE
	LDX HITTER	;X = HITTEE TYPE
;
HITXIT:	RTS
;
	.SBTTL COLLISION LOOKUP TABLES
;
; FIRST TABLE IS FOR LIKE OBJECT COLLISIONS
;
SAMHIT:	.WORD HIT2X2-1	;OBJ#0 MOSQUITOS
	.WORD NXTHIT-1	;OBJ#1 PLAYER
	.WORD HIT2X2-1	;OBJ#2 HORNET
	.WORD HIT3X3-1	;OBJ#3 EGGS
	.WORD NXTHIT-1	;OBJ#4 BULLETS
	.WORD NXTHIT-1	;OBJ#5 BONUS $ BUG
	.WORD HIT2X2-1	;OBJ#6 MUTANT
	.WORD HIT2X2-1	;OBJ#7 BEETLE
	.WORD NXTHIT-1	;OBJ#8 TORPEDO
	.WORD NXTHIT-1	;OBJ#9 THUNDER-BUG #1
	.WORD HIT2X2-1	;OBJ#A THUNDER-BUG #2
	.WORD NXTHIT-1	;OBJ#B (SPARE)
	.WORD NXTHIT-1	;OBJ#C (SPARE)
	.WORD NXTHIT-1	;OBJ#D GRENADE BUG 
	.WORD HITNXE-1	;OBJ#E VCB
	.WORD NXTHIT-1	;OBJ#F FFF
;
; THIS TABLE CONTAINS THE 1ST ENTRY NUMBER USED BY OBJECT #
;
; 0TH ENTRY IS FR OBJECT TYPE 0 VS 1-15
; 1ST ENTRY IS FOR OBJECT TYPE 1 VS 2-15
; ...
; LAST ENTRY IS FOR OBJECTY TYPE E VS F
;
HINDEX:	.BYTE 0,30.,58.,84.,108.,130.,150.,168.,184.
	.BYTE 198.,210.,220.,228.,234.,238.
;
; ALL COLLISION PROCESSES ARE ENTERED WITH
; THE FOLLOWING PARAMETERS
;
; <HITTER> HOLDS BASE ADDRESS OF THE SMALLER
;          OF THE TWO OBJECT TYPES
;
; <HITTEE> HOLDS BASE ADDRESS OF THE LARGER
;
; COLLISON PROCESSES USE (X) FOR HITTER INDEX AND (Y) FOR HITTEE
;
;
HITABL:	.WORD HIT0X1-1	;MOSQUITO / PLAYER
	.WORD HIT0X2-1	;MOSQUITO / HORNET
	.WORD NXTHIT-1	;MOSQUITO / EGGS
	.WORD HIT0X4-1	;MOSQUITO / BULLETS
	.WORD NXTHIT-1	;MOSQUITO / BONUS $ BUG
	.WORD NXTHIT-1	;MOSQUITO / MUTANT
	.WORD HIT0X7-1	;MOSQUITO / BEETLE
	.WORD NXTHIT-1	;MOSQUITO / TORPEDO
	.WORD NXTHIT-1	;MOSQUITO / THUNDER-BUG #1
	.WORD NXTHIT-1	;MOSQUITO / THUNDER-BUG #2
	.WORD NXTHIT-1	;MOSQUITO / OBJ#B
	.WORD NXTHIT-1	;MOSQUITO / OBJ#C
	.WORD NXTHIT-1	;MOSQUITO / GRENADE BUG
	.WORD HITNXE-1	;MOSQUITO / VCB
	.WORD HITNXF-1	;MOSQUITO / FFF
;
	.WORD HIT1X2-1	;PLAYER / HORNET
	.WORD HIT1X3-1	;PLAYER / EGG
	.WORD HIT1X4-1	;PLAYER / BULLET
	.WORD NXTHIT-1	;PLAYER / BONUS $ BUG
	.WORD KILLED-1	;PLAYER / MUTANT
	.WORD HIT1X7-1	;PLAYER / BEETLE
	.WORD KILLED-1	;PLAYER / TORPEDO
	.WORD HIT1X9-1	;PLAYER / THUNDER-BUG #1
	.WORD HIT1XA-1	;PLAYER / THUNDER-BUG #2
	.WORD HIT1XB-1	;PLAYER / CARRION
	.WORD NXTHIT-1	;PLAYER / OBJ#C
	.WORD HIT4XD-1	;PLAYER / GRENADE
	.WORD NXTHIT-1	;PLAYER / VCB
	.WORD HIT1XF-1	;PLAYER / FFF
;
	.WORD NXTHIT-1	;HORNET / EGG
	.WORD HIT2X4-1	;HORNET / BULLET
	.WORD NXTHIT-1	;HORNET / BONUS $ BUG
	.WORD NXTHIT-1	;HORNET / MUTANT
	.WORD HIT2X7-1	;HORNET / BEETLE
	.WORD NXTHIT-1	;HORNET / TORPEDO
	.WORD NXTHIT-1	;HORNET / THUNDER-BUG #1
	.WORD NXTHIT-1	;HORNET / THUNDER-BUG #2
	.WORD HIT2XB-1	;HORNET / CARRION OBJ#B
	.WORD NXTHIT-1	;HORNET / OBJ#C
	.WORD NXTHIT-1	;HORNET / GRENADE BUG
	.WORD HITNXE-1	;HORNET / VCB
	.WORD HITNXF-1	;HORNET / FFF
;
	.WORD HIT3X4-1	;EGG / BULLET
	.WORD NXTHIT-1	;EGG / BONUS $ BUG
	.WORD NXTHIT-1	;EGG / MUTANT
	.WORD NXTHIT-1	;EGG / BEETLE
	.WORD NXTHIT-1	;EGG / TORPEDO
	.WORD NXTHIT-1	;EGG / THUNDER-BUG #1
 	.WORD NXTHIT-1	;EGG / THUNDER-BUG #2
	.WORD NXTHIT-1	;EGG / OBJ#B
	.WORD NXTHIT-1	;EGG / OBJ#C
	.WORD NXTHIT-1	;EGG / GRENADE BUG
	.WORD NXTHIT-1	;EGG / VCB
	.WORD NXTHIT-1	;EGG / FFF
;
	.WORD HIT4X5-1	;BULLET / BONUS $ BUG
	.WORD LURCH-1	;BULLET / MUTANT
	.WORD HIT4X7-1	;BULLET / BEETLE
	.WORD HIT4X8-1	;BULLET / TORPEDO
	.WORD HIT4X9-1	;BULLET / THUNDER-BUG #1
	.WORD HIT4XA-1	;BULLET / THUNDER-BUG #2
	.WORD HIT4XB-1	;BULLET / OBJ#B
	.WORD NXTHIT-1	;BULLET / OBJ#C
	.WORD HIT4XD-1	;BULLET / GRENADE BUG
	.WORD HIT4XF-1	;BULLET / VCB
	.WORD HIT4XF-1	;BULLET / FFF
;
	.WORD NXTHIT-1	;BONUS $ BUG / MUTANT
	.WORD NXTHIT-1	;BONUS $ BUG / BEETLE
	.WORD NXTHIT-1	;BONUS $ BUG / TORPEDO
	.WORD NXTHIT-1	;BONUS $ BUG / THUNDER-BUG #1
	.WORD NXTHIT-1	;BONUS $ BUG / THUNDER-BUG #2
	.WORD NXTHIT-1	;BONUS $ BUG / OBJ#B
	.WORD NXTHIT-1	;BONUS $ BUG / OBJ#C
	.WORD NXTHIT-1	;BONUS $ BUG / GRENADE BUG
	.WORD HITNXE-1	;BONUS $ BUG / VCB
	.WORD HITNXF-1	;BONUS $ BUG / FFF
;
	.WORD NXTHIT-1	;MUTANT / BEETLE
	.WORD NXTHIT-1	;MUTANT / TORPEDO
	.WORD NXTHIT-1	;MUTANT / THUNDER-BUG #1
	.WORD NXTHIT-1	;MUTANT / THUNDER-BUG #2
	.WORD NXTHIT-1	;MUTANT / OBJ#B
	.WORD NXTHIT-1	;MUTANT / OBJ#C
	.WORD NXTHIT-1	;MUTANT / GRENADE BUG
	.WORD HITNXE-1	;MUTANT / VCB
	.WORD HITNXF-1	;MUTANT / FFF
;
	.WORD NXTHIT-1	;BEETLE / TORPEDO
	.WORD NXTHIT-1	;BEETLE/ THUNDER-BUG #1
	.WORD NXTHIT-1	;BEETLE / THUNDER-BUG #2
	.WORD HIT7XB-1	;BEETLE / CARRION OBJ#B
	.WORD NXTHIT-1	;BEETLE / OBJ#C
	.WORD NXTHIT-1	;BEETLE / GRENADE BUG
	.WORD HITNXE-1	;BEETLE / VCB
	.WORD HITNXF-1	;BEETLE / FFF
;
	.WORD NXTHIT-1	;TORPEDO / THUNDER-BUG #1
	.WORD NXTHIT-1	;TORPEDO / THUNDER-BUG #2
	.WORD NXTHIT-1	;TORPEDO / OBJ#B
	.WORD NXTHIT-1	;TORPEDO / OBJ#C
	.WORD NXTHIT-1	;TORPEDO / GRENADE BUG
	.WORD NXTHIT-1	;TORPEDO / VCB
	.WORD NXTHIT-1	;TORPEDO / FFF
;
	.WORD NXTHIT-1	;THUNDER-BUG #1 / THUNDER-BUG #2
	.WORD NXTHIT-1	;THUNDER-BUG #1 / OBJ#B
	.WORD NXTHIT-1	;THUNDER-BUG #1 / OBJ#C
	.WORD NXTHIT-1	;THUNDER-BUG #1 / GRENADE BUG
	.WORD NXTHIT-1	;THUNDER-BUG #1 / VCB
	.WORD NXTHIT-1	;THUNDER-BUG #1 / FFF
;
	.WORD NXTHIT-1	;THUNDER-BUG #2 / OBJ#B
	.WORD NXTHIT-1	;THUNDER-BUG #2 / OBJ#C
	.WORD NXTHIT-1	;THUNDER-BUG #2 / GRENADE BUG
	.WORD NXTHIT-1	;THUNDER-BUG #2 / VCB
	.WORD NXTHIT-1	;THUNDER-BUG #2 / FFF
;
	.WORD NXTHIT-1	;OBJ#B / OBJ#C
	.WORD NXTHIT-1	;OBJ#B / GRENADE BUG
	.WORD HITNXE-1	;OBJ#B / VCB
	.WORD HITNXF-1	;OBJ#B / FFF
;
	.WORD NXTHIT-1	;OBJ#C / GRENADE BUG
	.WORD NXTHIT-1	;OBJ#C / VCB
	.WORD NXTHIT-1	;OBJ#C / FFF
;
	.WORD HITNXE-1	;GRENADE BUG / VCB
	.WORD HITNXF-1	;GRENADE BUG / FFF
;
	.WORD HITNXF-1	;VCB / FFF
;
;
;
; NOTE THAT THESE COLLISION ROUTINES ARE NEVER ENTERED
; WITH DEAD OR DYING OBJECTS...
;
	.SBTTL MOSQUITO / PLAYER
;
; MOSQUITO / PLAYER COLLISION
;
HIT0X1:	TXA
	TAY	;<KILLED> NEEDS Y
	JMP KILLED
;
	.SBTTL MOSQUITO / HORNET
;
; MOSQUITO / HORNET COLLISION
;
HIT0X2:	JMP NXTHIT
;
	.SBTTL MOSQUITO / BULLET
;
; MOSQUITO / BULLET COLLISION
;
HIT0X4:	LDA X,OBJABX+1	;IF HIT OVER HOLE KILL IT
	STA TRIALX
	LDA X,OBJABY+1
	STA TRIALY
	LDA #7
	STA TRING
	JSR FITWEB
	BCS KLMSQ	;KILL HIM (OFF WEB)
	LDA #0
	STA TRING
	JSR FITMOR	;FALL IN HOLE?
	BCS MAK$X	;NOPE
;
KLMSQ:	LDA #DEAD
	STA Y,OBJFLG	;TURN OFF BULLET
	JSR TDINRX	;DID WE HIT A TARGET?
	LDA #0		;HIT MOSQ OVER HOLE NETS EAT
	JSR ANTSCR	;25 POINTS
	JMP NXTHIT
;
; MAKE A $ CARRION OUT OF OBJECT IN X
;
MAK$X:	LDA #ZBLACK	;TURN OFF PICTURE TEMPORARILY
	STA X,OBJVGC
	STA X,OBJVGC+6
	LDA #0B		;MAKE IT CARRION
	STA X,OBJAUX
	LDA #1F
	STA X,OBJDLY	;SO NO WIGGLING
	LDA #25		;HIT SOMETHING: 25 POINTS
	STY TEMP	;TURN OFF THE BULLET
	LDA #0
	STA X,OBJFLG	;SAY IN PERFECT HEALTH
	TAY
	JSR SCORIT
	LDY TEMP
OFFYWB:	JSR SNDPH
	LDA #DEAD
	STA Y,OBJFLG
	JMP NXTHIT
;
	.SBTTL MOSQUITO / MUTANT
;
; MOSQUITO / MUTANT COLLISION
;
HIT0X6:	JMP NXTHIT
;
	.SBTTL MOSQUITO / BEETLE
;
; MOSQUITO / BEETLE COLLISION
;
HIT0X7:	JMP NXTHIT
;
	.SBTTL BULLET / THUNDER-BUG #1
;
; BULLET / THUNDER-BUG #1
;
HIT4X9:	LDA Y,OBJAUX	;CHANGE TO THUNDER-BUG #2
	AND #0F0	;CHANGE #9 TO #A
	ORA #0A
	STA Y,OBJAUX
	LDA #1
	STA Y,OBJDLY
	LDA #DEAD	;KILL THE BULLET
	STA X,OBJFLG
	JSR SNDPH
	JMP NXTHIT
;
	.SBTTL BULLET / THUNDER-BUG #2
;
; BULLET / THUNDER-BUG #2
;
HIT4XA:	LDA #DEAD	;OFF BULLET
	STA X,OBJFLG
	INC NEGGS	;GOT EM ALL?
	LDA NEGGS
	CMP #11.
	BNE 10$		;NOPE
	JMP SC5000	;YEP! 5000 POINTS
10$:	TYA		;ANTSCR NEEDS X
	TAX
	LDA #9		;250 POINTS
	JSR ANTSCR
	JMP NXTHIT
;
SC5000:	LDX HITTEE
	STX TEMP	;SAVE FOR SESCRE
	LDA #0		;MAKE IT A FAKE MOSQUITO
	STA X,OBJAUX
	LDA #0		;5000 POINTS
	STA TRIALX	;00XXXX
	LDA #50		;XX50XX
	STA TRIALX+1
	LDA #0		;XXXX00
	STA TRIALX+2
	JSR SCVIAT
	JMP NXTHIT
;
; KILL OBJECT(X) FOR BCD POINTS IN TRIALX(MSB),+1,+2
;
SCVIAT:	LDA #60
	STA X,OBJVGC+1	;GET READY FOR MULTI DIGIT FALLING
	LDA #ZBLACK
	STA X,OBJVGC
	STA X,OBJVGC+6
	LDA TRIALX+2
	BIT ATRACT	;ATTRACT NO GETS POINTS
	BMI DUMMY
	SED
	CLC
	ADC SCORE+2
	STA SCORE+2
	LDA TRIALX+1
	ADC SCORE+1
	STA SCORE+1
	LDA TRIALX
	ADC SCORE
	STA SCORE
	CLD
	LDY #0		;DO BONUS LIFE STUFF
	LDA #0
	JSR SCORIT	
DUMMY:	JSR SESCRE	;LIGHT THE BOMB
	RTS
NOTALA:	TYA
	TAX
	LDA #0		;TBUGS ALL WORTH 25 OVER HOLE
	JSR ANTSCR	;25 POINTS
	JMP NXTHIT
;
; OBJECTS TYPE A (TBUG#2) ONLY THING THAT REALLY GETS STUNNED
;
STNOBA:	LDA #STUCK
	STA Y,OBJFLG
	LDA #20.
	STA Y,OBJDLY	;SO NO WIGGLING
	LDY #0
	LDA #25		;HIT T-BUG: 25 POINTS
	JSR SCORIT
OFFOBA:	JSR SNDPH
GONXT:	JMP NXTHIT
;
	.SBTTL PLAYER OR BULLET / GRENADE BUG
;
; PLAYER TOUCHES OR SHOOTS GRENADE BUG
;
HIT4XD:	LDA WALKIN	;IF BUG AROUND ON WALKIN : NO HITS
	BNE GONXT	;NO COLLISION ON WALK-IN
	LDA Y,OBJFLG
	AND #HEALTH
	BNE GONXT	;CAN'T HIT WHEN DYING OR ESCAPING
	LDA #STUCK	;'STUCK' MEANS START EXPLOSION
	STA Y,OBJFLG
	LDA #0
	STA Y,OBJRNG	;EXPL INDEX
	LDA #1
	STA Y,OBJDLY
	STA Y,OBJARM	;INCREMENT
	LDA #ZBLACK
	STA Y,OBJVGC	;TURN OFF FOR NOW
	STA Y,OBJVGC+6
	LDA SHWCIR	;PUT IN BOMB LINK
	STA Y,OBJVGC+4
	LDA SHWCIR+1
	STA Y,OBJVGC+5
;
; GETS 500 POINTS EVEN IF IT KILLS US...
;
	LDY #5
	LDA #0
	JSR SCORIT
	JMP NXTHIT
;
	.SBTTL PLAYER / HORNET
;
; PLAYER / HORNET COLLISION
;
HIT1X2:			;UNWEBBED HORNET KILLS PLAYER
KILLED:	LDA DIED	;NO COLLISION IF PLAYER DYING
	BNE BMBRTS	;CAN'T HAPPEN NOW
SNUFF:	LDA WALKIN	;OR WHILE WALKING IN
	BNE BMBRTS
	STY DIED	;SAVE WHO KILLED PLAYER
	JSR INISOU
	JSR SNDPP
	RTS
BMBRTS:	JMP NXTHIT	;NO COLLISION
;
; PLAYER /  FFF
;
HIT1XF:	LDA Y,OBJFLG	;NO COLLISION IF VCB/FFF ESCAPE
	AND #3
	BNE BMBRTS	;NO HITS
	JMP KILLED
	.SBTTL PLAYER / T-BUG #2
;
; PLAYER / T-BUG #2 COLLISION
;
HIT1XA:	JMP KILLED	;IT'S FATAL
	.SBTTL PLAYER / CARRION
;
; PLAYER / CARRION BLOB
;
HIT1XB:	JSR TDINRY	;MAYBE IT WAS THE VICTIM...
	TYA
	TAX
	LDA Y,OBJDLY
	AND #18
	LSR
	LSR
	LSR
	TAY
	LDA Y,BLBPNT	;BLOB VALUE
	JSR ANTSCR
	JMP NXTHIT
;
BLBPNT:	.BYTE 1,3,9,19.	;50,100,250,500
;
; HORNET / CARRION COLLISION
; (YES BILLY, THAT'S WHERE EGGS COME FROM)
;
HIT2XB:	LDA RANDOM	;50% OF EACH EGG TYPE
	BPL 10$		;DO A HORNET
	JMP HORNEY
10$:	LDA #8		;CAUSE HORNET X TO PAUSE
	STA X,OBJFLG
	JSR SNDHE	;HORNET SOUND
	LDA #20.
	STA X,OBJDLY
	LDA Y,OBJABX
	STA X,OBJABX
	LDA Y,OBJABX+1
	STA X,OBJABX+1
	LDA Y,OBJABY
	STA X,OBJABY
	LDA Y,OBJABY+1
	STA X,OBJABY+1
	LDA Y,OBJAUX
	AND #0F0
	ORA #3	;MUTANT EGG
	STA Y,OBJAUX
	STA Y,OBJARM	;SET ARM NZ FOR MUTANT EGG
	LDA #3		;MUTANT EGG COLLISION SIZE
	STA Y,OBJSIZ
	LDA #0
	STA Y,OBJFLG	;EGGS AREN'T 'STUCK'
	LDA #31.
	STA Y,OBJDLY
	LDA #0
	STA Y,OBJVGC+2
	LDA #73
	STA Y,OBJVGC+3
	STY HITTER
	LDA #0
	STA Y,OBJFLG
	JMP NXTHIT
HORNEY:	STX TEMP
	TYA
	TAX
	LDY TEMP
;
; ONLY LAY EGG IF <EVENTS> SAYS ITS OK
;
EGGSIT:	LDA #1		;HORNET EGG BIT
	AND EVENTS
	BEQ HT02XT	;CAN'T DO IT: IGNORE COLLISION
	JSR SNDHE	;HORNET SOUND
EGGER:	LDA Y,OBJFLG	;HORNET PAUSES TO LAY EGG
	ORA #8		;SET NO MOVE
	STA Y,OBJFLG
	LDA #20.
	STA Y,OBJDLY
	LDA X,OBJABX	;PUT HORNET OVER MOSQ
	STA Y,OBJABX
	LDA X,OBJABX+1
	STA Y,OBJABX+1
	LDA X,OBJABY
	STA Y,OBJABY
	LDA X,OBJABY+1
	STA Y,OBJABY+1
	LDA X,OBJAUX	;HORNET MAKES A EGG
	AND #0F0	;CLEAR OBJECT TYPE
	ORA #3		;MAKE IT AN EGG
	STA X,OBJAUX
	LDA #3		;SET EGG COLLISION SIZE
	STA X,OBJSIZ
	LDA #0		;FLAG WHICH KINDA EGG
	STA X,OBJARM	;0:HORNETS EGG
	STA X,OBJFLG
	LDA #31.
	STA X,OBJDLY	;TIME TILL IT HATCHES
	LDA #0
	STA X,OBJVGC+2	;INITIAL SCALE (PIX SIZE)
	LDA #73
	STA X,OBJVGC+3
	STX HITTER	;SHOW HIM A EGG PIX
HT02XT:	JMP NXTHIT
;
; BEETLE EATS A $....
;
HIT7XB:	LDA #DEAD	;OFF THE MOSQUITO
	STA Y,OBJFLG
	JSR SNDOE	;MUNCHING SOUND
	JMP NXTHIT
	
	.SBTTL PLAYER / EGG
;
; PLAYER / EGG COLLISION
;
HIT1X3:	LDA Y,OBJFLG
	AND #0F7	;CLEAR NO MOVE JUST IN CASE
	STA Y,OBJFLG
	STY HITTER
 	LDA Y,OBJHDG	;SAVE EGG ORIENTATION
	PHA
	LDA X,OBJDX	;LAST BULLET INDEX
	STA Y,OBJDX
	LDA X,OBJDY
	STA Y,OBJDY
	LDA X,OBJHDG	;BULLET HEADING
	STA Y,OBJHDG
	JSR MUTMUV	;NEEDS X AS INDEX
	LDX HITTER
	PLA
	STA X,OBJHDG	;RESTORE EGG HEADING
	JMP NXTHIT
;
	.SBTTL PLAYER / BULLET
;
; PLAYER / BULLET  USED FOR NEW LIFE WALKING IN
;
HIT1X4:	LDA WALKIN	;NEW LIFE WALKING IN?
	BEQ HT14XT	;NOPE, NO COLLISION
	LDA #0		;YEP, GIVE BACK THE HELM
	STA WALKIN
	STA NEGGS	;NO MULTIPLIER FOR WALKIN PUSHES
	STA NITBUG	;ALLOW EGGS TO RE-START
	BIT ATRACT
	BMI HT14XT	;GREEN SPIDER IN ATTRACT MOE
	LDA #0E0+ZRED
	STA OBJCLR+2
	STA OBJCLR+3
	LDA OBJHDG	;RESET KNOB TO HEADING
	EOR #0FF
	STA KNOB
HT14XT:	JMP NXTHIT
;
	.SBTTL BULLET / BONUS $ BUG
;
; BULLET / BONUS $ BUG
;
HIT4X5:	LDA #DEAD
	STA X,OBJFLG	;TURN OFF BULLET
	TYA		;ANTSCR NEEDS X REGISTER
	TAX
	LDA #34.	;DETERMINE BONUS IN BWFLY
	JSR ANTSCR	;SHOW FALLING POINTS
	JMP NXTHIT
;
	.SBTTL PLAYER / BEETLE
;
; PLAYER / BEETLE COLLISION
;
HIT1X7:	JMP KILLED
;
; TEST FOR PLAYER STEALING VCB
; MEAL... IF SO 1000 POINTS AND FLAG
; THAT VCB AS HAVING NO TARGET LEFT
; TO FORCE A RETARGET
;
TDINRX:	LDA ACTIVE	;ANY VCB/FFF AROUND?
	BEQ TDINXT	;NOPE
	STX TEMP	;WHICH BUG HE GOT
BEETUP:	STX TEMP1	;SAVED X REG
	STY TEMP2	;SAVED Y REG
	STA TEMP3	;COUNT DOWN # VCB/FFF
	TAX		;LAST ENTRY IN USE
ISFOOD:	LDA X,ACTIVE	;WHO IS THIS VCB
	TAY
	LDA Y,OBJARM	;HIS TARGETS NUMBER
	CMP TEMP	;GOT THIS ONE?
	BEQ RIPOFF	;BEAT HIM OUT...
	DEX		;LOOK AT THE NEXT IF THERE IS ONE
	DEC TEMP3	;NUMBER ALIVE
	BNE ISFOOD	;TRY THE NEXT
	LDX TEMP1	;RESTORE X
	LDY TEMP2	;RESTORE Y
TDINXT:	RTS
TDINRY:	LDA ACTIVE	;ANY VCB/FFF AROUND?
	BEQ TDINXT	;NOPE
	STY TEMP	;WHICH ONE HE GOT
	JMP BEETUP	;SEE IF IT QUALIFIED
RIPOFF:	LDA #0		;RE-TARGET THIS GUY
	STA Y,OBJARM
	PLA		;DISCARD RETURN ADDR
	PLA
	LDX TEMP	;WHICH ONE GOT IT
	LDA TAGGED
	ASL		;#TAGGED #2 (50 PER)
	ADC #23.	;600 POINTS BASE
	CMP #32.	;31ST ENTRY (800 POINTS) MAX
	BCC SCOROK
	LDA #31.
SCOROK:	JSR ANTSCR
	INC TAGGED	;INCREMENT # BEATEN OUT
	JMP NXTHIT
;
	.SBTTL PLAYER / FFF
;
; PLAYER / FFF
;
HIT1X8:	LDA WALKIN	;CAN'T COLLIDE ON WALKIN
	BEQ DOBOOM	;EXPLODE
GONEXT:	JMP NXTHIT
DOBOOM:	JMP KILLED	;THAT'S IT
;
	.SBTTL PLAYER / THUNDER-BUGS
;
; PLAYER / THUNDER BUGS
;
HIT1X9:	JMP KILLED
;
	.SBTTL HORNET / HORNET
;
; HORNET / HORNET COLLISION
;
HIT2X2:	LDA RANDOM	;PREVENTS PIED-PIPER PROBLEM
	AND #30
	CLC
	ADC X,OBJTGT
	AND #3F
	STA X,OBJTGT
	JMP NXTHIT
;
	.SBTTL EGG / EGG
;
; EGG / EGG COLLISION
;
; KEEPS EGGS FROM PILING UP
;
HIT3X3:	LDA X,OBJFLG
	AND #0F7	;CLEAR NO MOVE BIT
	STA X,OBJFLG
	JSR FLYMUV
	JMP NXTHIT
;
	.SBTTL HORNET / BULLET
;
; HORNET / BULLET COLLISION
;
HIT2X4:	JMP HIT0X4	;JUST LIKE MOSQUITO
;
	.SBTTL EGG / BULLET
;
; EGG / BULLET
;
HIT3X4:	LDA RANDOM	;PONG THE BULLET
	AND #87
	BPL PLSTWD
	ORA #0F8
PLSTWD:	CLC
	ADC Y,OBJHDG
	AND #3F
	STA Y,OBJHDG
	JSR SNDPH
	JMP NXTHIT
HIT4XB:	LDA RANDOM	;PONG THE BULLET
	AND #87
	BPL 10$
	ORA #0F8
10$:	CLC
	ADC X,OBJHDG
	AND #3F
	STA X,OBJHDG
	JSR SNDPH
	JMP NXTHIT
;
	.SBTTL HORNET / BEETLE
;
; HORNET / BEETLE COLLISION
;
HIT2X7:	JMP NXTHIT
;
	.SBTTL BULLET / MUTANT
;
; BULLET / ROCKETTE COLLISION
;
HIT4X8:	LDA #DEAD	;OFF BULLET
	STA X,OBJFLG
	TYA
	TAX
	LDA #32.	;1000 POINTS
	JSR ANTSCR
	JMP NXTHIT
;
	.SBTTL BULLET / BEETLE
;
; BULLET / BEETLE COLLISION
;
HIT4X7:	TYA
	PHA
	TXA
	TAY
	PLA
	TAX
	JMP HIT0X4	;JUST LIKE MOSQUITOS
;
	.SBTTL BULLET / VCB
;
; BULLET / FFF COLLISION
;
HIT4XF:	LDA Y,OBJFLG	;UNLESS STUNNED NO HITS
	AND #STUCK
	BNE GETOBC	;KILL THE BEAST
	LDA Y,OBJSPD	;HITTING SPEEDS HIM UP
	CLC
	ADC #1
	CMP #10
	BCS LURCH
	STA Y,OBJSPD
LURCH:	LDA #DEAD	;TURN OFF BULLET
	STA X,OBJFLG
 	LDA Y,OBJHDG
	PHA
	LDA X,OBJDX	;LAST BULLET INDEX
	STA Y,OBJDX
	LDA X,OBJDY
	STA Y,OBJDY
	LDA X,OBJHDG	;BULLET HEADING
	STA Y,OBJHDG
	STY HITTER
	JSR MUTMUV	;NEEDS X AS INDEX
	LDX HITTER
	PLA
	STA X,OBJHDG
	JSR SNDPH
	JMP NXTHIT
GETOBC:	LDA #DEAD
	STA X,OBJFLG
	LDA #0	;FAKE MOSQUITO
	STA Y,OBJAUX
	TYA
	TAX
	LDA #32.	;1000 POINTS
	JSR ANTSCR
NOPULP:	JMP NXTHIT
;
	.SBTTL INSECT / VCB
;
; SOMEBODY HAS HIT VACUUM, IT IT OUR TARGET?
;
HITNXE:	TXA		;HIT OUR TARGET?
	CMP Y,OBJARM
	BNE NOPULP	;NOPE
	LDA X,OBJFLG	;STILL ELIGIBLE?
	AND #HEALTH
	CMP #DYING
	BCS NOT2DY
;
; IF WE'VE HIT IT AND IT WAS OUR TARGET
; THEN IT MUST BE LEGAL, SO WE CAN KILL $ AS WELL...
;
	LDA #DEAD	;KILL IT
	STA X,OBJFLG
	LDA GROWTH	;MAKE HIM CRUISE FOR A WHILE
	ASL
	STA Y,OBJDLY
	LDA #0
	STA TAGGED	;RESET PLAYER MULIPLIER
	STA Y,OBJARM
	STA Y,OBJSPD	;SLOW US DOWN
	JSR SNDOE	;MUNCH
NOT2DY:	JMP NXTHIT
;
	.SBTTL INSECT / FFF
;
; SOMEBODY HAS HIT FFF, LAUNCH IT
;
HITNXF:	LDA X,OBJFLG	;STILL ELIGIBLE?
	AND #HEALTH
	CMP #DYING
	BCS NOT2DA
;
; IF WE HIT THE $ BUG IGNORE TILL COUNTER AT 45 SECONDS
;
	LDA X,OBJAUX
	AND #0F
	CMP #5	;$ BUG?
	BNE 10$	;NOPE
	LDA WVTIME
	CMP #30.	;CAN WE YET?
	BCS 10$		;YEP
	JMP NXTHIT
10$:	LDA X,OBJAUX	;MAKE IT A TORPEDO
	AND #0F0
	ORA #8
	STA X,OBJAUX
;
; REMOVE RING IN CASE OF EGG
;
	LDA #71
	STA X,OBJVGC+3
	STA X,OBJVGC+11.
	LDA #0FF	;WHEN HIS INTELLIGENCE
	STA X,OBJARM	;GETS A TURN IT TARGETS
	STA X,OBJSTP	;FOR LATER WAVES
	LDA #0
	STA X,OBJVGC+10.
	STA X,OBJFLG	;IN CASE IT WAS STUNNED
	STA X,OBJDLY	;SET FOR LATER WAVES WHEN TORPS SMART
	JSR SNDOO	;OH-OH SOUND
NOT2DA:	JMP NXTHIT
;
; SUBROUTINE TO TEST OBJECT OVER THE WEB
;
; INPUTS: X/Y TO BE TESTED IN TRIALX,TRIALX+1
;		AND TRIALY,TRIALY+1
;
; (RING#-1) TO TEST COLLISION WITH STORED IN <TRING>
;
FITWEB:	LDA TRIALX	;TEST SIGN OF X
	BPL TSGNY	;POSITIVE
	EOR #0FF
	STA TRIALX
	INC TRIALX
TSGNY:	LDA TRIALY
	BPL DOSUMZ
	EOR #0FF
	STA TRIALY
	INC TRIALY
DOSUMZ:	LDA TRIALX	;SAVE IN CASE WE'RE TRYING TWO RINGS
	STA TRIALX+1
	LDA TRIALY
	STA TRIALY+1
;
FITMOR:	STY TEMP
	LDA TRING
	ASL
	ASL
	ADC TRING
	ADC TRING	;6 BYTES PER
	TAY
	LDA Y,DIST2R+1	;TEST X LIMIT
	CMP TRIALX
	BCC OFFWEB	;NOPE
	LDA Y,DIST2R+3	;TEST Y LIMIT
	CMP TRIALY
	BCC OFFWEB
	LDA TRIALX
	LSR
	CLC
	ADC TRIALY
	CMP Y,DIST2R+1	;TEST ARM 0 Y+X/2
	BCS OFFWEB
	LDA TRIALX
	ASL
	CLC
	ADC TRIALY
	CMP Y,DIST2R+5	;TEST ARM 1 Y+2X
	BCS OFFWEB
ONWEB:	CLC
	LDY TEMP
	RTS
OFFWEB:	SEC
	LDY TEMP
	RTS
;
CENTER:	.BYTE 27,2C,30,34,39,0,7,0C,10,14,19,20
	.END
                                                     