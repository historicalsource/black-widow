	.TITLE BBSMRT
	.INCLUDE BBRAM
;
; THIS TABLE STRUCTURED PROGRESSIVE TORPEDO ROUTINE
;
;      *******************************************************
;      * COPYRIGHT 1982 ATARI. UNAUTHORIZED REPRODUCTION,    *
;      * ADAPTATION, DISTRIBUTION, PERFORMANCE OR DISPLAY OF *
;      * THIS COMPUTER PROGRAM OR THE ASSOCIATED AUDIOVISUAL *
;      * WORK IS STRICTLY PROHIBITED!!!!!!!                  *
;      *******************************************************
;
; EXTERNAL REFERENCES
;
	.GLOBL SUMS5	;CHECKSUM A000-AFFF
	.GLOBL SCORCL	;TABLE FOR SCORES/GRENADES
	.GLOBL SNDEX	;GRENADE BOOM EVERY RING INCREMENT
	.GLOBL SNDPF	;PLAYER SHOOTS
	.GLOBL LITEM	;SHOW # LIVES
	.GLOBL DANCES	;DANCE STEPS FOR THUNDER BUGS
	.GLOBL DEATH	;PLAYER CASHES IN OUT OF SEQUENCE
	.GLOBL FLYTRN	;TURN A FLYING OBJ
	.GLOBL FLYMUV	;MOVE A FLYING OBJ
	.GLOBL SMELL	;SNIFF-OUT A TARGET
	.GLOBL MUTMUV	;MUTANT MOVEMENT
	.GLOBL ATAN	;ARC-TANGENT ROUTINE
;
; GLOBAL DEFINITIONS
;
	.GLOBL SMART	;OBJECT INTELLEGENCE
	.GLOBL HDGFLT
	.GLOBL OBJTYP	;ATTRIBUTE TABLE
;
	.GLOBL WIGGLE
	.GLOBL SEEOBJ
	.GLOBL SCLINK
	.GLOBL SHWNST
;
	.GLOBL WHOCLR
	.GLOBL WHOSIZ	;FOR GROWING EGGS
	.SBTTL BBANIM FORMER MODULE
	.INCLUDE VGMC16
;
; EXTERNAL REFERENCES
;
;
; WIGGLE - UPDATE LEG/WING/JAW PICTURE #'S
; PICTURES ARE SEQUENCED 0 , 1 , 2 , 1 , 0
; REPEATEDLY
;
CHKSM5::	.WORD SUMS5	;CHECKSUM FOR A000-AFFF
WIGGLE:	LDA X,OBJAUX	;GET COUNT & UP/DOWN FLAG
	TAY
	AND #20		;0=UP 1=DOWN
	BEQ WGLUP	;INCR PIX #
	TYA
	SEC
	SBC #40
	TAY
	AND #0C0
	BEQ WGLZIP	;WE'RE NOW AT 0
PYWGL:	TYA
PAWGL:	STA X,OBJAUX	;RESTORE PIX#
	RTS
WGLZIP:	TYA
	AND #1F		;COUNT UP FROM 0
	JMP PAWGL
WGLUP:	TYA
	CLC
	ADC #40
	TAY
	AND #0C0
	CMP #0C0
	BNE PYWGL
	TYA
	AND #1F
	ORA #60		;COUNT DOWN FROM 1
	TAY
	AND #0F		;WEBS(OBJECT#4) COUNT 0,1,2,0,1,2
	CMP #4		;OTHERS COUNT 0,1,2,1,0,1,2
	BNE PYWGL	;NOT A WEB
	TYA
	AND #1F
	JMP PAWGL
;
;
;
	.SBTTL IMAGE SEQUENCE # & REFLECTION PROCESS
;
; PERFORMED EVERY FRAME WHILE GENERATOR IS PAINTING THE WEB PICTURE
;
; THIS PROCESS TAKES AN OBJECT BASE ADDRESS
; IN <X> AND A HEADING IN <X,OBJHDG> AND
; UPDATES <OBJVGC> WITH THE CORRECT OBJECT
; PICTURES AS DETERMINED BY <OBJAUX> OBJECT
; TYPE IN 4 LSB'S.  THE PICTURE SUBROUTINE LINKED INTO
; THE DISPLAY LIST IS ALSO A FUNCTION OF <HOWLUK> AND THE
; CURRENT PHASE NUMBER (<X,OBJAUX> 2 MSB'S) FOR ANIMATIONS.
; TABLE <HOWLUK> DESCRIBES THE DYNAMIC PORTION (IF ANY) OF
; THE OBJECT... AS IN LEGS OR LEGS&WINGS ETC
;
; BODY ROTATIONS LINKAGES ARE INDEXED AS FOLLOWS
;
; TTTTHHHH0  T=OBJ TYPE H=4-BIT HEADING 0=WORD BOUNDARIES
;
; ANIMATION LINKAGES AS FOLLOWS
;
; SSHHHH00
;
TWIST:	CPX #0		;PLAYER IS DIFFERENT
	BNE SAMDIR
	LDA TBHD
	JMP PLYDIR
SAMDIR:	LDA X,OBJHDG
PLYDIR:	CLC
	ADC #2		;ROUND TO NEAREST MULTIPLE OF 22.5 DEGREES
	LSR
	LSR
	AND #0F
	STA TEMP	;TEMP = ROUNDED HEADING
	LDA X,OBJAUX	;GET OBJECT TYPE
	STA TEMP+1	;SAVE TYPE
	AND #0F
	CMP #3		;NESTS ARE 1 OF TWO KINDS
	BEQ 33$
	PHA
	LDA #0
	STA X,OBJVGC+10.
	LDA #71
	STA X,OBJVGC+11.
	PLA
	JMP NTNST
33$:	LDA X,OBJARM	;STORAGE FOR HORNET OR MUTANT NESTS
	BNE MPIC
	LDA TEMP+1
	AND #0F0
	ORA #2
	JMP FAKEH
MPIC:	LDA TEMP+1
	AND #0F0
	ORA #6
FAKEH:	STA TEMP+1
	AND #0F
;
; FOR BULLET, $ BUG AND $ CARRION ONLY 1 ROTATION
;
NTNST:	CMP #5		;DOLLAR SIGN HAS ONLY 1 PICTURE
	BNE 10$		;NOPE
	LDA #50
	BNE NRMWNG	;FIXED BODY WITH ROTATED WINGS
10$:	CMP #0B		;CARRION SPECIAL
	BEQ SNFLE	;YEP
	CMP #4		;AS ARE BULLETS
	BNE NTDLR	;NOPE
	LDA TEMP+1	;GET PHASE NUMBER
	AND #0C0
	LSR
	LSR
	LSR
	LSR
	LSR
	LSR
	ORA #40
	JMP NRMWNG	;SHOW SPINNING SPIKED BALL
SNFLE:	LDY #0		;FORCE A HEADING OF 0 FOR SINGLE ROTATION
	STY TEMP
NTDLR:	ASL
	ASL
	ASL
	ASL
	ORA TEMP	;COMBINE OBJECT TYPE WITH HEADING
;
NRMWNG:	STA X,OBJVGC+4
	LDA #0A4
	STA X,OBJVGC+5	;'JSRL' OP CODE TO 28XX OR 29XX
;
DOSTEP:	ASL TEMP	;TEMP = ANGLE# * 2
	LDA TEMP+1	;GET WING/LEG PHASE NUMBER
	AND #0C0
	LSR
	ORA TEMP
	STA TEMP	;LSB OF LINK TABLE ADDRESS
;
; X = SSHHHH0
;
	LDA TEMP+1	;GET PICTURE TYPE
	AND #0F
	ASL
	TAY
	LDA Y,HOWLUK+1	;ANY DYNAMIC PORTION?
	BMI 10$		;NOPE
	LDA Y,HOWLUK	;PAGE NUMBER/2
	ORA TEMP
	STA X,OBJVGC+8
;
; IF NO DYNAMIC PARTS FOR THIS TYPE THIS ENTRY WILL BE ZERO
;
	LDA Y,HOWLUK+1
	CLC
	ADC #0A5	;JSRL TO 2A00
	STA X,OBJVGC+9
10$:	RTS
;
; OBJECT TYPE VS LINE-SET FOR PICTURE
; LOOKUP TABLE (INDEXED BY OBJECT #)
;
HOWLUK:	.WORD 100 	;#0 MOSQUITO : WINGS/LEGS
	.WORD 0 	;#1 SPIDER : LEGS
	.WORD 100 	;#2 HORNET : WINGS/LEGS
	.WORD 0FFFF	;#3 EGG UNUSABLE (SEE TWIST)
	.WORD 0FFFF 	;#4 BULLET UNUSABLE (SEE TWIST)
	.WORD 100 	;#5 $ BUG : WINGS/LEGS
	.WORD 180 	;#6 MUTANT : JAWS/WINGS/LEGS
	.WORD 80 	;#7 BEETLE : JAWS/LEGS
	.WORD 200 	;#8 TORPEDO : WINGS
	.WORD 200 	;#9 T-BUGS : WINGS/LEGS
	.WORD 200 	;#A T-BUGS : WINGS/LEGS
	.WORD 0FFFF 	;#B : CARRION NO DYNAMICS
	.WORD 100 	;#C : WINGS/LEGS
	.WORD 200 	;#D GRENADE : WINGS
	.WORD 180 	;#E VCB : JAWS/WINGS/LEGS
	.WORD 200 	;#F FFF : WINGS
;
;
; SEEOBJ - UPDATE VG DISPLAY LIST
;
; AND TURN-OFF ALL DEAD OBJECTS
;
SEEOBJ:	LDX #0
	LDY #0		;POINTER INTO OBJVGA
SEEONE:	LDA X,OBJFLG	;IS THIS ONE ALIVE?
	AND #HEALTH
	CMP #DYING	;LIVE & STUCK SEE COLOR
	BCC LEAVON	;00 OR 01 = ALIVE OR STUNNED
;
	CMP #DEAD
	BEQ CORPSE	;THIS ONES INACTIVE
;
	CPX #0		;THE PLAYER
	BEQ SPCLSP
	JMP KABOOM
;
SPCLSP:	LDA #0
	JMP LEAVON
CORPSE:	LDA #0		;CENTER DEAD OBJECTS
	STA Y,OBJVGA
	STA Y,OBJVGA+1
	STA Y,OBJVGA+2
	STA Y,OBJVGA+3
	STA X,OBJABX	;PUT IN CENTER HERE TOO
	STA X,OBJABX+1
	STA X,OBJABY
	STA X,OBJABY+1
;
; IN CASE SCORES OR EXPLOSIONS PRESENT WHEN START PRESSED
; RESET EVERYTHING IN THE DISPLAY LIST
;
	LDA #60
	STA X,OBJVGC+7
	LDA #ZBLACK
	STA X,OBJVGC	;TURN BODY OFF
	STA X,OBJVGC+6	;AND LEGS/WINGS ALSO
	LDA #0A4	;**BODIES**
	STA X,OBJVGC+5	;IN CASE WERE SHOWING SCORE
	LDA #0AF
	STA X,OBJVGC+9
	LDA #0
	STA X,OBJVGC+8	;IN CASE WERE SEEING SCORE
	LDA #0
	STA X,OBJVGC+4
	LDA #0
	STA X,OBJVGC+10.
	LDA #71
	STA X,OBJVGC+11.
	STA X,OBJVGC+3
	JMP CHKNXT
;
LEAVON:	AND #HEALTH
	BEQ 10$		;HEALTHY
	LDA X,OBJAUX	;IS IT A GRENADE?
	AND #0F
	CMP #0D
	BNE 10$
	JMP CHKNXT	;YEP: EXPLOSION IN PROGRESS
10$:	JSR TWIST	;SHOW APPROPRIATE PICTURE
	TXA
	LSR
	TAY		;FOR OBJVGA INDEXING
;
; UPDATE POSITION VECTORS IN DISPLAY LIST
;
	LDA X,OBJABX
	STA VGX
	LDA X,OBJABX+1
	STA VGX+1
	BMI XISNEG
	LDA #0
	BEQ XISPOS
XISNEG:	LDA #0FF
XISPOS:	ASL VGX
	ROL VGX+1
	ROL
	ASL VGX
	ROL VGX+1
	ROL
	AND #1F
	STA Y,OBJVGA+3
	LDA VGX+1
	STA Y,OBJVGA+2
	LDA X,OBJABY
	STA VGY
	LDA X,OBJABY+1
	STA VGY+1
	BMI YISNEG
	LDA #0
	BEQ YISPOS
YISNEG:	LDA #0FF
YISPOS:	ASL VGY
	ROL VGY+1
	ROL
	ASL VGY
	ROL VGY+1
	ROL
	AND #1F
	STA Y,OBJVGA+1
	LDA VGY+1
	STA Y,OBJVGA
	LDA X,OBJAUX	;LOOK UP THIS GUYS COLOR
	AND #0F
	CMP #3		;NESTS ARE VERY UNUSUAL
	BNE 31$
	JMP NESTER
31$:	CMP #5		;FLYING $ (HUGE)
	BEQ DOLLAR	;DOING BONUS BUG
	CMP #0E		;VCB/FFF ARE YELLOW IF STUCK
	BCC NOTDLR
	ASL
	TAY
	LDA X,OBJFLG	;IF STUCK THEN YELLOW
	AND #3
	CMP #STUCK
	BNE PIXSIZ
	LDA #0E0+ZYELLO
	STA X,OBJVGC
	STA X,OBJVGC+6
	JMP SIZEE
DOLLAR:	LDA #70
	STA X,OBJVGC+3
	LDA #71
	STA X,OBJVGC+11.
	LDA #0
	STA X,OBJVGC+2
	STA X,OBJVGC+10.
	LDA #<0E0!ZGREEN>
	STA X,OBJVGC
	LDA #<080!ZWHITE>
	STA X,OBJVGC+6
	JMP CHKNXT
GOCARN:	JMP CARION
NOTDLR:	ASL
	TAY
	CMP #16		;B BUG CARRION COLOR DEPENDS ON SIZE
	BEQ GOCARN
PIXSIZ:	CPY #8		;BULLETS SPARKLE
	BNE OBCOLR	;NOPE
	LDA RANDOM
	AND #7
	ORA #0C0
	CMP #0C0
	BNE SETACO
	LDA #0C7
	BNE SETACO
SETACO:	STA X,OBJVGC
	LDA Y,OBJCLR+1
	STA X,OBJVGC+6
	LDA $TOUGH
	CMP #3
	BNE SIZEE
	LDA #0C
	STA X,OBJSIZ
	LDA #20
	STA X,OBJVGC+2
	JMP CHKNXT
OBCOLR:	LDA Y,OBJCLR	;OBJECTS COLOR
	STA X,OBJVGC
	LDA Y,OBJCLR+1
	STA X,OBJVGC+6
SIZEE:	LDA Y,WHOSIZ+1
	STA X,OBJSIZ
	LDA Y,WHOSIZ
	STA X,OBJVGC+2
	JMP CHKNXT
NESTER:	LDA SHWNST	;PUT IN THE RING
	STA X,OBJVGC+10.
	LDA SHWNST+1
	STA X,OBJVGC+11.
	LDY #4.		;IN THE EVENT THIS IS MUTANT
	LDA X,OBJARM	;HORNET OR MUTANT GROWING?
	BEQ FOOLIE	;IT'S A HORNET
	LDY #12.	;IT'S A MUTANT
FOOLIE:	LDA Y,OBJCLR	;OBJECTS COLOR
	STA X,OBJVGC
	LDA Y,OBJCLR+1
	STA X,OBJVGC+6
	JMP CHKNXT
KABOOM:	CMP #1		;IF STUCK LEAVE ALONE
	BEQ CHKNXT
	LDA X,OBJAUX	;DOES THIS ONE EXPLODE OR SCORE?
	AND #0F
	TAY
	LDA Y,OBJTYP	;SMART WILL HANDLE BOTH KINDS
	BPL NUMBER	;MSB 0 = SCORE
CHKNXT:	TXA
	CLC
	ADC #10
	TAX
	LSR		;OBJVGA IS ONLY 8 BYTES PER
	TAY
	BEQ SEERTS	;THAT'S ALL
	JMP SEEONE
NUMBER:	LDA FRMCNT	;COLORS CHANGE NOT TOOO FAST
	AND #03
	BNE CHKNXT
	LDA RANDOM
	AND #7
	TAY
	LDA Y,SCORCL
	STA X,OBJVGC
	JMP CHKNXT
SEERTS:	RTS
;
SCORCL::	.BYTE 0E7,0E1,0E1,0E2,0E3,0E4,0E5,0E6,0E7
CARION:	LDA X,OBJDLY
	AND #18
	LSR
	LSR
	LSR
	STY TEMP
	TAY
	LDA Y,CARCOL	;CARRION COLOR
	STA X,OBJVGC
	LDA #ZBLACK
	STA X,OBJVGC+6
	LDY TEMP
	JMP SIZEE
CARCOL:	.BYTE 0E0!ZBLUE,0E0!ZGREEN,0E0!ZTURQ,0E0!ZWHITE	;BLUE GREEN TURQ WHITE
;
	.NLIST
	.LIST
	.RADIX 16
;
; OBJECT TYPE VS BODY/LEG COLOR LOOKUP TABLE
; FOLLOWING BODY/LEG COLOR IS BYTE OF
; 'SCAL' INSTRUCTION AND ACTUAL
; SIZE TO BE USED IN COLLISION PROCESS
;
WHOCLR:	.BYTE 0C0!ZPURPL,0C0!ZGREEN	;#0 MOSQUITO
	.BYTE 0E0!ZRED,0E0!ZRED		;#1 PLAYER
	.BYTE 0A0!ZRED,0A0!ZYELLO	;#2 HORNET
	.BYTE 0E0!ZPURPL,ZBLACK		;#3 EGG
	.BYTE 0C0!ZWHITE,ZBLACK		;#4 BULLET
	.BYTE 0E0!FGREEN,080!ZWHITE	;#5 $ BONUS BUG
	.BYTE 0E0!ZPURPL,0A0!ZRED	;#6 MUTANT
	.BYTE 0E0!ZBLUE,080!ZRED	;#7 BEETLE
	.BYTE 0C0!ZWHITE,0E0!ZRED	;#8 TORPEDO
	.BYTE 0E0!ZYELLO,0E0!ZRED	;#9 T-BUG #1
	.BYTE 0E0!ZRED,0E0!ZBLUE	;#A T-BUG #2
	.BYTE 0E0!ZGREEN,ZBLACK		;#B
	.BYTE ZBLACK,ZBLACK		;#C
	.BYTE 0E0!ZRED,0E0!ZGREEN	;#D GRENADE
	.BYTE 0C0!ZPURPL,0A0!ZGREEN	;#E VCB
	.BYTE 0C0!ZWHITE,0E0!ZRED	;#F FFF
;
WHOSIZ:	.BYTE 7F,2		;#0 MOSQUITO SCAL/SIZE
	.BYTE 20,3		;#1 PLAYER SCAL/SIZE
	.BYTE 60,3		;#2 HORNET SCAL/SIZE
	.BYTE 0,4		;#3 NEST SCAL/SIZE
	.BYTE 60,2		;#4 BULLET SCAL/SIZE
	.BYTE 0,4		;#5 BONUS $ BUG SCAL/SIZE
	.BYTE 0,4		;#6 MUTANT SCAL/SIZE
	.BYTE 0,4		;#7 BEETLE SCAL/SIZE
	.BYTE 60,3		;#8 TORPEDO SCAL/SIZE
	.BYTE 3F,4		;#9 T-BUG #1 SCAL/SIZE
	.BYTE 3F,4		;#A T-BUG #2 SCAL/SIZE
	.BYTE 0,4		;#B SPARE
	.BYTE 0,4		;#C SPARE
	.BYTE 0,4		;#D GRENADE SCAL/SIZE
	.BYTE 0,4		;#E VCB SCAL/SIZE
	.BYTE 0,4		;#F FFF SCAL/SIZE
;
;
	.SBTTL <SMART> HOSTILE MOTION
;SMART - OBJECT MOTION ROUTINES
;
SMART:	LDX #0		;STARTING WITH 1ST OBJECT
	STX BADGUY	;ATTRACT MODE PLAYER TARGETS
	STX MOSQUE	;LET 1ST MOSQUITO LEAD
	STX ACTIVE	;NUMBER ACTIVE FFF'S OR VCB'S
;
SMTLUP:	STX HITTER
	LDA X,OBJFLG
	AND #HEALTH	;IS CURRENT OBJECT DEAD?
	CMP #DYING	;ONLY PROCESS LIVE OR STUNNED
	BCC ITZALV	;STILL 'ALIVE'
;
	BEQ DKNELL	;DEATH IN PROGRESS
;
TRYNXT:	JMP NXTSMT	;DEAD
;
ITZALV:	LDA X,OBJAUX	;GET CRITTER TYPE
	AND #0F
	TAY		;LOOKUP TYPE TO TELL IF
	LDA Y,OBJTYP	;IT'S A PLAYER TARGET
	AND #20
	BNE NOTBAD	;DON'T WORRY ABOUT IT
			;WHEN ATTRACT MODE PLAYER
	INC BADGUY
NOTBAD:	TYA
	ASL
	TAY		;USE AS INDEX TO ADDR TABLE
	LDA Y,MOTION+1
	PHA		;BUILD A RETURN ADDRESS
	LDA Y,MOTION
	PHA
	RTS
;
; ALL OBJECTS EXPLODE/SCORE THE SAME WAY
; (EXCEPTION: GRENADE BUG PROGRESSIVE KILLER)
;
DKNELL:	TXA	;SET X&Y TO OBJECT
	BEQ TRYNXT	;PROCESS ALL BUGS THEN TEST PLAYER
;
; DETERMINE IF THIS TYPE EXPLODES OR SCORES
;
	LDA X,OBJAUX
	AND #0F
	TAY
	LDA Y,OBJTYP
	BMI KABLAM	;THIS GUY BLOWS UP
;
; SEE SCORE FALLING
;
	LDA X,OBJVGC+2
	CLC
	ADC #10
	STA X,OBJVGC+2
	BPL NXTSMT
	LDA #0
	STA X,OBJVGC+2
	INC X,OBJVGC+3
	LDA X,OBJVGC+3
	CMP #74
	BCC NXTSMT
	JSR KILBUG	;OFF THE SUCKER
	JMP NXTSMT
;
KABLAM:	JSR BOMBED	;UPDATE EXPLOSION PICTURE
;
NXTSMT:	LDA HITTER	;INCREMENT TO NEXT OBJECT
	CLC
	ADC #10
	TAX
	BCS SMTXXT
	JMP SMTLUP
;
; IF NONE BUT VCBS OR FFFS AROUND THEN
; HOLD EGG DEVELOPMENT TO MAKE THEM LEAVE
;
SMTXXT:	LDA NITBUG	;HAVE DECIDED VCB LEAVING?
	BNE TGONE	;SEE IF THEY'RE GONE YET
	LDA ACTIVE	;VCB GONE ALREADY?
	BEQ NOSTUN
	LDA BADGUY	;TARGETS FOR US?
	ORA BAD2GO	;OR YET TO BE PLANTED?
	BNE NOSTUN	;YEP
	LDA #0FF	;YEP, MAKE 'EM LEAVE FOR GOOD
	STA NITBUG	;HOLD OFF EGGS WHILE IT LEAVES
	JMP NOSTUN
TGONE:	LDA ACTIVE	;ALL GONE YET?
	ORA BADGUY
	BNE NOSTUN	;NOPE
	STA NITBUG
NOSTUN:	LDA OBJFLG	;IF PLAYER DYING, WAIT FOR ALL BUGS TO ESCAPE
	CMP #DYING
	BNE NOCROK	;NOT PLAYER DEATH
	LDA #1		;HALT EGGS
	STA NITBUG	;& EXIT VCB/FFF
	LDA RANDOM
	ORA #0E0
	STA OBJVGC
	LDA OBJHDG
	CLC
	ADC #3
	AND #3F
	STA OBJHDG
	LDX #0
	JSR SAMDIR	;SPINS THE PLAYER
	LDA BADGUY	;NUMBER MOVING OBJECTS
	ORA ACTIVE	;#FFFS OR VCBS ALIVE
	BNE NOCROK
;
; WAIT FOR DEATH SOUND TO FINISH
;
	LDA POINT+8+5
	BNE NOCROK
	LDA #DEAD
	STA OBJFLG
NOCROK:	RTS
;
; THIS SUBROUTINE USED BY NORMAL EXPLOSIONS
; AND GRENADE PROGRESSIVE KILLER EXPLOSION
;
BOMBED:	LDA X,OBJRNG	;INDEX FOR SIZE OF RING	;INDEX TO SCAL/STAT
	ASL		;4 BYTES/ENTRY
	ASL
	TAY
	LDA Y,EXPSIZ	;GET COLOR OF THIS PIX
	STA X,OBJVGC
	LDA Y,EXPSIZ+1	;STAT BITS THIS PIX
	STA X,OBJVGC+1
	LDA Y,EXPSIZ+2	;LINEAR SCALE
	STA X,OBJVGC+2
	LDA Y,EXPSIZ+3	;BINARY SCALE
	STA X,OBJVGC+3
	LDA X,OBJRNG	;INCR/DECR INDEX
	CLC
	ADC X,OBJARM
	AND #7
	STA X,OBJRNG
	BNE MORBUG	;MORE IN SEQUENCE
	LDA X,OBJARM
	BMI KILBUG	;THAT'S IT
	LDA #0FF
	STA X,OBJARM	;MAKE INCR-1
	LDA #08
	STA X,OBJRNG
MORBUG:	RTS
;
KILBUG:	SEI		;JUST IN CASE
	LDY HITTER
	LDA #ZBLACK
	STA Y,OBJVGC
	STA Y,OBJVGC+6
	LDA #60
	STA Y,OBJVGC+5
	LDA #0		;IN CASE WAS SCORE
	STA Y,OBJVGC+4
	LDA #0A4
	STA Y,OBJVGC+5
	LDA #60		;RESTORE STAT IN CASE AN EGG OR MUTANT
	STA Y,OBJVGC+7
	LDA #71
	STA Y,OBJVGC+3
	LDA #DEAD
	STA X,OBJFLG
	CLI
	RTS
;
; OBJECT MOTION DETERMINATION LINK TABLE
;
MOTION:	.WORD IOBJ0-1	;OBJ#0 MOSQUITO CLOUD
	.WORD IOBJ1-1	;OBJ#1 PLAYER MOVEMENT & SHOOTING
	.WORD IOBJ2-1	;OBJ#2 HORNET
	.WORD IOBJ3-1	;OBJ#3 EGGS
	.WORD IOBJ4-1	;OBJ#4 BULLET
	.WORD IOBJ5-1	;OBJ#5 BONUS $ BUG
	.WORD IOBJ6-1	;OBJ#6 MUTANT
	.WORD IOBJ7-1	;OBJ#7 BEETLE
	.WORD IOBJ8-1	;OBJ#8 TORPEDO
	.WORD IOBJ9-1	;OBJ#9 THUNDER-BUG #1
	.WORD IOBJA-1	;OBJ#A THUNDER-BUG #2
	.WORD IOBJB-1	;OBJ#B CARRION BLOB
	.WORD IOBJC-1	;OBJ#C SPARE
	.WORD IOBJD-1	;OBJ#D GRENADE
	.WORD IOBJE-1	;OBJ#E VACUUM-CLEANER (VCB)
	.WORD IOBJF-1	;OBJ#F FICKLE-FINGER (FFF)
;
;
; PLAYER INTELLIGENCE
;
IOBJ1:	LDA WALKIN	;ARE WE WALKING IN?
	BEQ NRMPLY	;NOPE
	LDA #10.		;MAKE HIM FAST
	STA OBJSPD
	LDY #50
	JSR DEDEYE	;POINT AT SOMETHING
	LDA OBJTGT	;ON AUTO WALKIN NO STEERING HYSTERESIS
	STA OBJHDG
	JMP PLAUTO	;INTERRUPT IS STEERING US
NRMPLY:	LDA #3
	STA OBJSPD
	BIT ATRACT	;IN ATTRACT?
	BPL USESTK	;NOPE: READ LEFT STICK
	LDA #5
	STA OBJSPD
	LDY #50
	JSR DEDEYE	;STEER FOR HIM
	LDA MINHDG
	BMI NOTRUK	;NOTHING TO SHOOT STOP
	LDA OBJTGT	;NO HYSTERESIS
	STA OBJHDG
	LDA BUGSHW
	CMP #6
	BCS AUTSHT	;DON'T MOVE IN GRENADE SEQUENCE
PLAUTO:	LDX #0
	JSR FLYTRN	;TURN THE PLAYER
	LDA WALKIN	;WALK-IN IS  F A S T
	BNE MUVPLY
	LDA RANDOM	;POKEY RANDOM#
	AND #3
	BEQ AUTSHT	;DON'T MOVE
MUVPLY:	JSR FLYMUV	;MOVE THE SPIDER ABOUT
AUTSHT:	JSR SHOOT
NOTRUK:	JMP NXTSMT
;
; JOYSTICK STATE TO HEADING LOOKUP
; NEGATIVE VALUE INDICATES INVALID
; SPEED GOES UP AT 48 AND EVERY 4 THEREAFTER
; TO MAX OF 8
;
USESTK:	LDA RESPED
	STA OBJSPD
	LDA FLAGS2	;USE LEFT (MOVE) STICK
	AND #JYMASK	;ISOLATE JOYSTICK BITS
	EOR #JYMASK	;IF CENTERED TO MOTION
	BEQ PLYMXT	;NO MOVEMENT
	TAY
	LDA Y,STICKS	;VALID COMBO?
	BMI PLYMXT	;INVALID COMBINATION
	STA OBJHDG
	JSR FLYTRN
	JSR FLYMUV	;MOVE PLAYER
PLYMXT:	JSR SHOOT
	JMP NXTSMT
;
; STICK STATE TO HEADING LOOPUP
; JOYSTICKS ARE UDLR
;
STICKS:	.BYTE 80	;0000 ILLEGAL
	.BYTE 10	;0001 RIGHT
	.BYTE 30	;0010 LEFT
	.BYTE 80	;0011 LEFT/RIGHT ILLEGAL
	.BYTE 20	;0100 DOWN
	.BYTE 18	;0101 DOWN/RIGHT
	.BYTE 28	;0110 DOWN/LEFT
	.BYTE 80	;0111 DOWN/LEFT/RIGHT ILLEGAL
	.BYTE 0		;1000 UP
	.BYTE 8		;1001 UP/RIGHT
	.BYTE 38	;1010 UP/LEFT
	.BYTE 80	;1011 UP/LEFT/RIGHT ILLEGAL
	.BYTE 80	;1100 UP/DOWN ILLEGAL
	.BYTE 80	;1101 UP/DOWN/RIGHT ILLEGAL
	.BYTE 80	;1110 UP/DOWN/LEFT ILLEGAL
	.BYTE 80	;1111 UP/DOWN/LEFT/RIGHT ILLEGAL
;
;SHOOTING ROUTINE
FNXWEB:	TXA
	CLC
	ADC #10
	TAX
	CMP #50			;LIMIT PLAYER TO 4 WEBS
	BNE SNWEB
	LDA #0		;OUT OF BULLETS, CAN SHOOT AS SOON
	STA TRIGER	;AS GET ONE BACK
	RTS
;
;
;IF THE PLAYER IS SHOOTING - FIND AN AVAILABLE WEB, SET ITS
;X&Y TO THAT OF THE PLAYER - LOOK UP ITS MOTION DX&DY FROM
;PLAYER'S HEADING - REFERENCE REFLECTION FOR SIGN CHANGES
;
SHOOT:	LDA WALKIN
	BNE SHTRTS
	BIT ATRACT
	BPL TRIGR	;NOT IN ATTRACT
	LDA OBJTGT
	STA TBHD
	LDA BUGSHW	;IF EGG PUSH OR $ TAG THEN DON'T SHOOT
	CMP #7
	BEQ ATSHT
	CMP #2
	BCC SHTRTS
	CMP #4
	BCS SHTRTS
	JMP ATSHT
SHTRTS:	RTS
TRIGR:	LDA FLAGS3	;FIRESTICK MOVED?
	AND #JYMASK
	EOR #JYMASK
	BNE BLAST	;HE'S FIRING
	LDA #0
	STA TRIGER	;SAY IT'S OK TO SHOOT
	RTS
BLAST:	TAY
	LDA Y,STICKS	;VALID COMBO?
	BMI SHTRTS	;NOPE, NO SHOOTING
	STA TBHD	;SAVE BULLET COURSE
	DEC TRIGER
	BPL SHTRTS
	LDA #2
	STA TRIGER
ATSHT:	LDX #10
;
;SCAN OBJECT TABLE FOR AN IDLE WEB TO FIRE
;
SNWEB:	LDA X,OBJFLG	;GET FLAGS OF THIS WEB
	AND #DEAD	;IS WEB ACTIVE?
	BEQ FNXWEB	;THIS ONE CURRENTLY IN FLIGHT
PWEB:	STX HITTER	;PRETEND DOING BULLET
	LDA TBHD	;STORE STICK DIRECTION
	STA OBJSTP	;USE AS PLAYER ORIENTATION
	STA X,OBJHDG
	BIT ATRACT	;ATRACT SHOOTING DIFFERENT
	BPL 10$
	LDA OBJHDG
	STA X,OBJHDG
10$:	JSR SNDPF	;SHOOT SOUND
	LDA #1
	STA X,OBJDLY		;USE TARGETING DELAY FOR WEB AGE
	LDA OBJABX		;COPY PLAYER DELTA X&Y TO WEB
	STA X,OBJABX
	LDA OBJABX+1
	STA X,OBJABX+1
	LDA OBJABY
	STA X,OBJABY
	LDA OBJABY+1
	STA X,OBJABY+1
	LDA #0		;MAKE BULLET ALIVE
	STA X,OBJFLG
	JSR FLYTRN
	LDA #0
	STA HITTER	;DON'T CONFUSE NXTSMT
	RTS
;
; MOSQUITO MOTION EMULATION
;
; <MOSQUE> = + NO GROUP INTELLIGENCE
;		- LS 6 BITS= LEADERS HEADING
;
; <MOSQUE+1> = ANIMATION
;
; <MOSQUE+1> = SPEED SETTING
;
IOBJ0:	JSR ESCAPE	;ARE WE FLEEING?
	LDA MOSQUE	;LEADER PICKED YET?
	BEQ MASTER	;NOPE- WE'LL DECIDE
	TAY
	DEC X,OBJDLY
	BPL 10$
	LDA GROWTH
	ORA #1
	STA X,OBJDLY
	LDA #0FF
	STA MINHDG
	LSR
	STA MINRNG
	JSR SMELL
	LDX HITTER
	STA X,OBJTGT
	LDA MOSQUE+1	;ANIMATION AS WELL
	STA X,OBJAUX
	LDA MOSQUE+2	;AND SPEED
	CLC
	ADC #1
	AND #0F
	BNE 66$
	LDA #0F
66$:	STA X,OBJSPD
10$:	JMP SLAVE	;DO IT LIKE ALL OTHERS
MASTER:	STX MOSQUE
	DEC X,OBJDLY	;TIME TO RE-"TARGET"?
	BPL IFLYC	;NOPE
	LDA RANDOM	;RANDOMLY ATTACK
	BPL DRIFT	;EVERY OTHER TIME ATTACK
	LDA #0FF
	STA MINHDG
	LSR
	STA MINRNG
	LDY #0		;FIND PLAYER
	JSR SMELL
	LDX HITTER
	JMP PUTHD
DRIFT:	LDA RANDOM
	AND #3F
PUTHD:	STA X,OBJTGT	;ESTABLISH NEW ARBITRARY HEADING
	LDA GROWTH	;WAVE DETERMINES SPEED
	ASL
	ASL
	ORA #1
	STA X,OBJDLY
IFLYC:	LDA X,OBJAUX
	STA MOSQUE+1
	LDA X,OBJSPD
	STA MOSQUE+2
SLAVE:	JSR HDGFLT
	JSR FLYTRN
	JSR FLYMUV	;COMPUTE DX/DY FROM HEADING
IFLYXT:	JMP NXTSMT
;
; FLYING HORNET : EGG LAYING
;
IOBJ2:	JSR ESCAPE	;ESCAPE MAYBE
	DEC X,OBJDLY	;TIME TO RETARGET
	BNE SMHTGT
	LDA X,OBJFLG	;CLEAR NO MOVE
	AND #0F7
	STA X,OBJFLG
	LDA GROWTH
	ORA #1
	STA X,OBJDLY
	JSR HRNSNF	;FIND STUCKEE, OR PLAYER
	LDX HITTER
	STA X,OBJTGT
SMHTGT:	JSR HDGFLT
	JSR FLYTRN	;FLYING OBJECT TURN FILTER
	JSR FLYMUV	;FLYING OBJECT MOVEMENT
	JMP NXTSMT
;
; GROWING HORNET OR MUTANT EGG
;
IOBJ3:	BIT ATRACT	;IF HELP MODE NO GROW
	BMI SMLPIX
	LDA DIED
	BEQ GROWR
	LDA #32.	;RESET EGG SIZES
	STA X,OBJDLY
	JMP GROWN
GROWR:	LDA NITBUG	;FFF/VCB EXITS? HOLD OFF
	BNE NSXIT
	LDA FRMCNT
	AND #0E
	BNE NSXIT
	DEC X,OBJDLY	;COUNT-DOWN GESTATION PERIOD
	BMI HATCH	;EGG HATCHES
;
; EGG GROWS
;
GROWN:	LDA X,OBJARM
	BEQ SMLPIX
	LDA X,OBJDLY
	JMP BIGPIX
;
SMLPIX:	LDA X,OBJDLY
	CLC
	ADC #12.
BIGPIX:	TAY
	AND #30
	LSR
	LSR
	LSR
	LSR
	CLC
	ADC #71
	STA X,OBJVGC+3
	TYA
	AND #0F
	ASL
	ASL
	ASL
	STA X,OBJVGC+2
	JMP TWINKL	;WIGGLE WHEN NEAR HATCHING
HATCH:	LDA #2		;MAKE A HORNET
	STA X,OBJAUX
	LDA X,OBJARM	;SHOULD IT BE A MUTANT?
	BEQ GRWN	;NOPE, A HORNET WILL DO
	LDA #6
	STA X,OBJAUX
GRWN:	LDA #1		;INITIAL TARGETING DELAY
	STA X,OBJDLY
	LDA #0
	STA X,OBJFLG
	LDA #71		;REMOVE THE RING
	STA X,OBJVGC+11.
	LDA #0
	STA X,OBJVGC+10.
NSXIT:	JMP NXTSMT
;
; CARRION BLOB OBJECT B
;
IOBJB:	BIT ATRACT	;IF HELP MODE NO DECAY
	BMI NSXIT
	LDA #0
	STA X,OBJSPD
	LDA FRMCNT	;TWINKLE VIA FRAME COUNTER
	AND #7
	BNE RTNESC
	DEC X,OBJDLY
	BPL RTNESC	;BLOB DECAYS
	LDA #DEAD
	STA X,OBJFLG
RTNESC:	JMP NXTSMT
;
; MUTANT WALL-KILLER PLAYER-HUNTER
;
IOBJ6:	JSR ESCAPE	;ARE WE FLEEING
	LDA ACTIVE
	ORA BADASS+0E
	ORA BADASS+0F
	BNE 23$
	LDA OBJSPD	;GO AT PLAYER SPEED
	STA X,OBJSPD
	JMP MUTFLY
23$:	DEC X,OBJDLY
	BMI GETHIM	;RETARGET TIME
	JMP MUTFLY
;
; MUTANT TRACKS THE PLAYER
;
GETHIM:	LDA OBJSPD	;1 CLICK SLOWER THAN PLAYER
	SEC
	SBC #1
	STA X,OBJSPD
	LDA GROWTH
	ORA #1
	STA X,OBJDLY
	LDA #0FF
	STA MINRNG
	LDY #0
	JSR SMELL	;TARGET ON PLAYER
	LDX HITTER
	STA X,OBJTGT
MUTFLY:	JSR HDGFLT
	JSR FLYTRN
	JSR FLYMUV
	JMP NXTSMT
;
; FLYING $ BONUS BUG
; HAS SAME INTELLEGENCE AS GRENADE
;
IOBJ5:	JSR ESCAPE
	DEC X,OBJDLY
	BPL TRCKR
	LDA GROWTH
	STA X,OBJDLY
	JSR POLISH
TRCKR:	JSR HDGFLT
	JSR FLYTRN
	JSR FLYMUV
	JMP NXTSMT
;
;
; BULLETS ARE 'SMART' IN THAT THEY TIME-OUT
;
IOBJ4:	INC X,OBJDLY
	LDA X,OBJDLY	;COUNT UP FROM 0
	CMP #24.	;FORTY-EIGHT FRAMES IS ALL
	BCC IWBXT	;NOT TIME YET
	LDA #DEAD
	STA X,OBJFLG
	JMP NXTSMT
IWBXT:	LDA X,OBJHDG
	JSR FLYTRN
	JSR FLYMUV	;MOVE THE WEB SHOT
	JMP NXTSMT
;
; BEETLE EATING-MACHINE METHOD
;
IOBJ7:	JSR ESCAPE	;STUCK?
;
; WE CAN MOVE, FIND A TARGET
;
	DEC X,OBJDLY
	BNE SMBTGT
	LDA GROWTH
	ORA #1
	STA X,OBJDLY
	JSR SNIFF	;FIND SOME FOOD
	LDX HITTER
	STA X,OBJTGT
SMBTGT:	JSR HDGFLT
	JSR FLYTRN
	JSR FLYMUV
	JMP NXTSMT
;
;
; FFF-LAUNCHED TORPEDO INTELLIGENCE
; THESE ARE THE DEADLIES...
; FIRST APPEAR IN WAVE 17
; FROM 17 THRU 28 THEY FLY STRAIGHT AT WHERE PLAYER  W A S
; 29 THRU 36 THEY BURST THEN FLY STRAIGHT AT WHERE PLAYER W A S
; 37 TO 52 TARGET MORE AND MORE OFTEN
; 53 ON TURN TIGHTER AND TIGHTER CIRCLES
;
IOBJ8:	JSR ESCAPE	;PLAYER MAY BE DYING
	INC X,OBJSPD
	LDA X,OBJSPD
	CMP #10
	BCC FASREK
	DEC X,OBJSPD
;
FASREK:	LDA BINWAV	;AFTER WAVE 34 TRACK ALL TIME
	CMP #28.	;STILL VERY DUMB ROCKETTES?
	BCS KILROK	;NOPE... NOT TOO DUMB
;
; TARGET ONCE AT PLAYER AT BIRTH THEN FLY STRAIGHT
;
	LDA X,OBJARM	;ALREADY TARGETED?
	BPL TORPER	;YEP
	LDA #2		;ALWAYS TURN AT LEAST THIS FAST
	STA X,OBJARM
	LDA #0		;LARGEST CIRCLE AT START
	STA X,OBJRNG
	JSR FINDEM	;RETURN PLAYER BEARING IN A
	STA X,OBJHDG	;SNAP TURN AT HIS FACE
TORPER:	JSR FLYTRN	;GO AT CURRENT HEADING
	JSR FLYMUV
	JMP NXTSMT
;
; DO STAR BURST THEN ATTACK
;
KILROK:	LDA X,OBJSTP	;HAVE WE VEERED YET?
	BPL SWATER	;YEP
	LDA #0
	STA X,OBJSTP
	STA X,OBJRNG	;CIRCLE INDEX
	JSR FINDEM
	STA X,OBJTGT	;BEARING ONCE WE FINISH
;
; UP TO 45 THEY TAKE +/- 45 DEGREES
; AFTER THAT +/- 90 DEGREE JOGS
;
	LDA BINWAV
	CMP #45.
	BCC 71$
	LDA RANDOM
	AND #8F
	BPL 45$
	AND #0F
	JMP 38$
71$:	LDA RANDOM
	AND #87
	BPL 45$
	AND #07
38$:	EOR #0FF
	CLC
	ADC #1
45$:	CLC
	ADC X,OBJTGT
	AND #3F
	STA X,OBJHDG
	LDA #8		;HOW LONG TO VEER
	STA X,OBJDLY
	JMP TORPER	;GO ON CURRENT HEADING
SWATER:	LDA X,OBJARM	;STILL STALLING?
	BPL 2$		;NOPE
	DEC X,OBJDLY	;FEIGNING DELAY
	BPL TORPER	;STILL OFF IN RANDOM DIRECTION
	LDA #1		;TURN AT LEAST THIS FAST
	STA X,OBJARM
	LDA #0		;IF DUMMIE SET MINIMUM TURNING RATE
	STA X,OBJRNG
	LDA BINWAV	;HOW SMART DOES HE START OUT?
	SEC
	SBC #37.
	BMI NOTURN	;NOT TIGHTENING YET
	CMP #32.
	BCC 11$		;NOT AT TIGHTEST CIRCLE YET
	LDA #31.	;SMALLEST CIRCLE
11$:	STA X,OBJRNG	;INDEX INTO SMTORP
2$:	DEC X,OBJDLY	;TIME FOR NEW TARGET?
	BPL NOTURN	;TORPER
	INC X,OBJSTP	;COUNT NUMBER OF PASSES AT THIS SETTING
	LDA X,OBJSTP
	CMP #5
	BCC 99$		;NOT 5 YET
	LDA #0
	STA X,OBJSTP
	LDA X,OBJRNG	;TIME FOR NEW ENTRY?
	CLC
	ADC #20
	BCC 93$		;NOPE
	CLC
	ADC #1
	AND #1F
	BNE 93$		;NOT OFF END YET
	LDA #31.
93$:	STA X,OBJRNG
99$:	LDA X,OBJRNG
	AND #1F
	ASL
	TAY
	LDA Y,SMTORP
	STA X,OBJDLY
	LDA Y,SMTORP+1
	STA X,OBJARM
	JSR FINDEM
	STA X,OBJTGT
NOTURN:	LDA X,OBJARM
	STA TEMP1
	JSR HFLITE
	LDA #0
	STA X,OBJARM
	JSR FLYTRN
	JSR FLYMUV
	JMP NXTSMT
;
; RETURNS HEADING TO PLAYER IN A
;
FINDEM:	LDA #0FF	;DETERMINE PLAYER HEADING
	STA MINHDG
	LSR
	STA MINRNG
	LDY #0
	JSR SMELL
	LDX HITTER
	RTS
;
SMTORP:	.BYTE 7,1,7,1,6,1,6,1,5,1,5,1,4,1,4,1,4,2,4,2,4,3,4,3,4,4,4,4
	.BYTE 4,5,4,5,4,6,3,6,3,7,3,8,3,9,3,10.
	.BYTE 2,8,2,9,1,7,1,8,1,9,1,10.,1,11.,1,12.,1,13.,1,14.
;
; ACROBATIC BUG INTELLIGENCE
;
IOBJ9:	LDA DIED
	BNE HAULIT
	LDA WALKIN
	BEQ NOHAUL
;
; IF PLAYER DYING OR WALKING IN LET EVERYBODY HAUL ASS
;
HAULIT:	LDA #0F
	STA X,OBJSPD
	LDA #STUCK
	ORA X,OBJFLG
	STA X,OBJFLG
;
; IS THIS MANEOUVER COMPLETED?
;
NOHAUL: DEC X,OBJDLY	;TIME FOR NEW MOVE?
	BEQ STEER	;YES
	LDA X,OBJFLG	;IF NEGATIVE USE HDGFLT TO FIND PLAYER
	BPL SMOOTH
ATTACK:	JSR HDGFLT
	JMP TRNBUG
SMOOTH:	LDA X,OBJHDG
	CLC
	ADC X,OBJSTP
	AND #3F
	STA X,OBJHDG
TRNBUG:	JSR FLYTRN
	JSR FLYMUV
	JMP NXTSMT
;
; IF WE'VE JUST TIMED OUT, CHECK TO SEE IF
; WE WERE HUNTING, IF LAST HUNTER IN LINE
; THEN CLEAR INSYNC
;
STEER:	LDA X,OBJARM
	STA FROM
	LDA X,OBJRNG
	STA FROM+1
	JSR WHOZAT	;SET <LEADER> TO WHO'S IN FRONT
	LDA X,OBJFLG
	BPL PATERN	;WEREN'T HUNTING
	TXA
	TAY
LIVBUG:	STY TEMP	;TEMP HOLDS LAST LIVING OBJECT
DEDBUG:	TYA
	CLC
	ADC #10
	BNE FNDLST	;NOT LOOKING AT #11
	CPX TEMP	;IS LAST LIVING US?
	BNE NOHUNT
	LDA #0
	STA INSYNC
	JMP NOHUNT
FNDLST:	TAY
	LDA Y,OBJAUX
	AND #0F
	CMP #9
	BNE DEDBUG
	LDA Y,OBJFLG	;WE NEED LAST LIVING BUG
	AND #3
	BEQ LIVBUG
	JMP DEDBUG
;
FIELDA:	STA X,OBJDLY	;ESTABLISH COUNT
	INY
	LDA NY,FROM	;GET HEADING DELTA
	STA X,OBJSTP
	LDA #2		;2 BYTES PER ENTRY IN DANCER
	CLC
	ADC FROM	;NEW TABLE ADDRESS
	STA X,OBJARM
	LDA #0
	ADC FROM+1
	STA X,OBJRNG
	JMP SMOOTH	;PROCEED WITH MANEOUVER
;
; IF WE WERE BUT ARE NO LONGER HUNTING... ONLY THE LEADER CAN
; PICK A NEW DANCE ROUTINE FOR THE LINE
;
NOHUNT:	LDA X,OBJFLG
	AND #7F
	STA X,OBJFLG
	LDA LEADER	;ARE WE IN FRONT
	CMP HITTER
	BEQ PIKDNC	;YEP, PICK NEXT STEP
	LDA DANCEA	;USE THE LEADERS COICE
	STA FROM
	LDA DANCEA+1
	STA FROM+1
PATERN:	LDY #0
	LDA NY,FROM
	BPL FIELDA	;TREAT AS HEADING OFFSET
	LDA #80		;SET THE HUNTING BIT
	ORA X,OBJFLG
	STA X,OBJFLG
;
; DANCERS SPEED UP WITH EVERY PASS MADE AT THE PLAYER
;
	INC X,OBJSPD
	LDA X,OBJSPD	;WHEN AT MAX SPEED 'STUCK' SO LEAVE
	CMP #10
	BNE 10$
	DEC X,OBJSPD
	LDA #STUCK
	ORA X,OBJFLG
	STA X,OBJFLG
10$:	CPX LEADER	;ARE WE THE LEADER?
	BEQ FINDER	;NOPE
	LDA TARGET	;FOLLOW LEADERS HEADING
	STA X,OBJTGT
	JMP ALREDY
;
; THE LEADER GETS TO PICK THE NEXT DANCE
;
PIKDNC:	LDY #0		;WE'RE THE LEADER... WHATS NEXT FOR US
	LDA NY,FROM
	BNE NOENDT	;NOT TABLE END
	LDAL DANCES
	STA FROM
	STA DANCEA
	LDAH DANCES
	STA FROM+1
	STA DANCEA+1
	JMP PIKDNC	;TEST FOR LEGIT PATTERN
NOENDT:	BMI ISAHNT	;ATTACK PHASE IS NEXT
DANCOK:	LDA FROM
	STA DANCEA
	LDA FROM+1
	STA DANCEA+1
	JMP PATERN
ISAHNT:	LDA INSYNC	;ANYBODY STILL HUNTING?
	BEQ DANCOK	;NOPE, ATTACK IS OK
	LDA #2
	CLC
	ADC FROM
	STA FROM
	LDA #0
	ADC FROM+1
	STA FROM+1
	JMP PIKDNC
FINDER:	LDA #7F
	STA INSYNC
	STA MINRNG
	LDY #0
	JSR SMELL
	AND #3F
	LDX HITTER
	STA X,OBJTGT
	STA TARGET
;
ALREDY:	LDY #1
	LDA NY,FROM
	STA X,OBJDLY	;DELAY FOR TARGETING
	LDA #2
	CLC
	ADC FROM
	STA X,OBJARM
	LDA #0
	ADC FROM+1
	STA X,OBJRNG
	JMP ATTACK
;
; SET <LEADER> TO OBJECT INDEX THAT IS IN FRONT
;
WHOZAT:	LDY #50
	CPX #50		;IF 1ST OBJECT THEN OBVIOUSLY THE LEADER
	BEQ ITZHIM	;PICK NEXT STEP
ARE1ST:	LDA Y,OBJFLG
	ORA Y,OBJAUX
	AND #0F
	CMP #9
	BEQ ITZHIM	;FOUND A LIVING 9
	TYA
	CLC
	ADC #10
	TAY
	CPY HITTER	;ABOUT TO LOOK AT US?
	BCC ARE1ST	;KEEP LOOKING
ITZHIM:	STY LEADER	;SAVE WHO LEADER IS
	RTS
;
; IRRATE THUNDER BUG BECOMES OBJECT #A
;
IOBJA:	JSR ESCAPE	;ESCAPE IF POSSIBLE
;
; IF NO DANCERS (TYPE 9) AROUND THEN LEAVE
;
	LDY #50
ANY9S:	LDA Y,OBJAUX	;COMBINE 9 WITH ALIVE
	ORA Y,OBJFLG
	AND #0F
	CMP #9		;FOUND A 9?
	BEQ NORMA	;YEP
	TYA
	CLC
	ADC #10
	TAY
	BNE ANY9S	;LOOK AGAIN
;
; NO TYPE NINES LEFT... EXIT
;
	JMP GOAWAY
;
NORMA:	DEC X,OBJDLY
	BPL SMHDG	;NO RE-TARGET
	LDA GROWTH
	ORA #1
	STA X,OBJDLY
	LDA #7F
	STA MINRNG
	LDY #0	;GET THE PLAYER
	JSR SMELL
	AND #3F
	LDX HITTER
	STA X,OBJTGT
SMHDG:	JSR HDGFLT
	JSR FLYTRN
	JSR FLYMUV
	JMP NXTSMT
SEEBMB:	LDA X,OBJRNG	;SHOW HIM THE RING BEFORE COLLISION
	CMP TEMP	;MAX SIZE PER DIFFICULTY
	BCS XPLDUN
	ASL
	ADC X,OBJRNG
	TAY
	LDA Y,GNDSIZ+1
	STA X,OBJVGC+2
	LDA Y,GNDSIZ+2
	STA X,OBJVGC+3
	JMP NXTSMT
;
; GRENADE : NEVER 'DIES'
; WHEN STUNNED, EXPLOSION SIZE DETERMINES
; INCREASING KILL ZONE
;
IOBJD:	LDA $TOUGH	;SET MAX EXPANSION SIZE VIA DIFFICULTY
	CMP #3
	BNE 34$
	LDA #1		;FAKE A MEDIUM
34$:	CLC
	ADC #6
	STA TEMP	;6=EASY 7=MED 8=HARD
	LDA DIED	;PLAYER DIED?
	BEQ NRMBMB	;NOPE
FLIGHT:	LDA X,OBJFLG	;IF WE'RE EXPLODING, FINISH
	AND #HEALTH
	CMP #STUCK
	BEQ TIMBMB	;CONTINUE BLOWING UP
	JMP GOAWAY
NRMBMB:	LDA WALKIN
	BNE FLIGHT
	LDA X,OBJFLG	;ARE WE 'STUCK'?
	AND #HEALTH
	BNE TIMBMB	;YEP.. BLOW UP
	JMP NOBOMB	;NOPE
;
TIMBMB:	DEC X,OBJDLY
	BPL SEEBMB	;DON'T BLOW UP TOO FAST
	LDA $TOUGH
	CMP #3
	BNE 82$
	LDA #4
	JMP 83$
82$:	SEC
	LDA #5		;DELAY BETWEEN RINGS 3=HARD 4=MED 5=EASY
	SBC $TOUGH
83$:	STA X,OBJDLY
	LDA X,OBJRNG	;USE RNG FOR GRAPHICS & KILL RADIUS INDEX
	CMP TEMP	;DONE LAST RING, TIME 2 DIE?
	BCC GRWING	;NOPE
XPLDUN:	JSR KILBUG	;KILL THE GRENADE AT LAST
	JMP NXTSMT
GRWING:	ASL
	ADC X,OBJRNG	;3 BYTES EACH
	TAY
	LDA Y,GNDSIZ	;KILL RADIUS,4 BYTES STAT,SCAL
	STA MINRNG	;KILLING RADIUS THIS PICTURE
	LDA #60		;ALWAYS NORMAL STAT
	STA X,OBJVGC+1
	LDA RANDOM
	AND #7
	TAY		;PICK A RANDOM COLOR FOR RINGS
	LDA Y,SCORCL
	STA X,OBJVGC
	INC X,OBJRNG
	LDA #80
	STA STING
	LDA #0		;SCAN ALL OBJECTS
SPLATR:	TAY
	PHA		;SAVE INDEX
	CPY HITTER	;CAN'T KILL OURSELF
	BEQ NXTOFF
	LDA Y,OBJAUX	;LEAVE BULLETS ALONE
	AND #0F
	CMP #4
	BEQ NXTOFF	;IT'S A BULLET
	LDA Y,OBJFLG	;LEAVE DEAD/DYING ALONE
	AND #HEALTH
	CMP #DYING	;LEAVE ALONE IF DEAD/DYING
	BCS NXTOFF	;DEAD OR DYING ALREADY
	CMP #STUCK	;IF A GRENADE LEAVE IT ALONE
	BNE TRYBMB	;TO EXPLODE IN PIECE
	LDA Y,OBJAUX
	AND #0F
	CMP #0D
	BEQ NXTOFF
TRYBMB:	LDX HITTER	;DISTANCE FROM NITRO
	JSR SMELL	;DO RANGE CHECK
;
; ANYTHING WITHIN RANGE WILL BEGIN DYING FOR 500 POINTS
;
NXTOFF:	PLA
	CLC
	ADC #10
	TAY
	BNE SPLATR
 	LDA #0		;CLEAR STING FOR OTHER SMELLING
	STA STING
	JSR SNDEX	;BOOM EVERY RING
BMBXIT:	JMP NXTSMT
NOBOMB:	DEC X,OBJDLY
	BPL TRACKR
	LDA GROWTH
	STA X,OBJDLY
	JSR POLISH
TRACKR:	JSR HDGFLT
	JSR FLYTRN
	JSR FLYMUV
	JMP NXTSMT
;
; FLYING FICKLE FINGER FFF INTELLIGENCE
;
IOBJF:	INC ACTIVE	;INCREMENT NUMBER OF FFF'S
	LDY ACTIVE	;SAVE WHERE WE ARE
	TXA
	STA Y,ACTIVE
	LDA NITBUG	;TIME TO GO?
	BEQ OBJFT	;NOPE
	BPL OBFCMB	;WE'LL BE BACK
	LDA #STUCK	;YELLOW: LEAVE FOREVER
	STA X,OBJFLG
OBFCMB:	JMP GOAWAY	;LEAVE THE SCREEN
OBJFT:	DEC X,OBJDLY
	BPL TRACKD
	LDA GROWTH
	LSR
	ORA #1
	STA X,OBJDLY
	LDA #0FF
	STA MINHDG
	LSR
	STA MINRNG
	JSR POLAR
TRACKD:	LDA FRMCNT	;SPEED UP EVERY 256 FRAMES
	BNE NOFAST
	LDA X,OBJSPD
	CLC
	ADC #1
	CMP #10
	BCS NOFAST
	STA X,OBJSPD
NOFAST:	LDA X,OBJSPD
	STA TEMP
TRNFFF:	JSR HDGFLT
	DEC TEMP
	BPL TRNFFF
	JSR FLYTRN
	JSR FLYMUV
	JMP NXTSMT
;
; VACUUM CLEANER (VCB) INTELLIGENCE
;
IOBJE:	INC ACTIVE	;INCREMENT NUMBER OF VCB'S
	LDY ACTIVE	;SAVE WHERE WE ARE
	TXA
	STA Y,ACTIVE
	LDA NITBUG	;TIME TO GO?
	BEQ OBJET	;NOPE
	BPL OBECMB	;WE'LL BE BACK
	LDA #STUCK	;YELLOW: LEAVE FOREVER
	STA X,OBJFLG
OBECMB:	JMP GOAWAY	;LEAVE THE SCREEN
OBJET:	LDA X,OBJARM	;GOT A TARGET?
	BNE BANZAI	;YEP
;
; NO TARGET... AMBLE AND MAYBE PICK ONE
;
CRUISE:	LDA #0		;SLOW DOWN AND DWADDLE
	STA X,OBJSPD
	DEC X,OBJDLY
	BMI CLEVER
	LDA RANDOM	;PICK NEW DUMB HEADING
	AND #3F		;EVERY ONCE IN A WHILE
	BNE NOFLIT	;NOT YET
	LDA RANDOM	;PICK A RANDOM DIRECTION
	AND #3F
	STA X,OBJTGT
NOFLIT:	JMP TRNVCB	;DRIFT RANDOMLY
;
; PICK A RANDOM NON-PLAYER OBJECT AND
; MAKE IT OUR TARGET.
;
CLEVER:	LDA #30		;DELAY ONCE WE PICK ONE
	STA X,OBJDLY	;TO GIVE PLAYER A WARNING
	LDA #50		;START WITH FIRST NON-BULLET
SELTGT:	STA X,OBJARM	;ARM HOLDS CURRENT SAMPLE
	TAY
	JSR LEGIT	;HOW BOUT THIS ONE?
	BCS SETTGT	;GOT ONE
	TYA
	CLC
	ADC #10
	BCC SELTGT	;TRY AGAIN
;
; NOW WE'VE SCANNED ALL POSSIBLES...CRUISE
;
DUMBO:	LDA #0
	STA X,OBJARM	;INDICATE NO TARGET
	STA X,OBJSPD
	LDA GROWTH
	ASL
	STA X,OBJDLY
	JMP NXTSMT
;
; FOUND A TARGET : ESTABLISH IT
;
SETTGT:	LDA #0FF
	STA MINHDG
	LSR
	STA MINRNG
	JSR SMELL
	LDX HITTER
	LDA MINHDG
	STA X,OBJHDG
	JMP PAWSIT	;GET THE PIG
;
; GOT A TARGET, GET IT...
;
BANZAI:	TAY
	JSR LEGIT	;TARGET STILL THERE?
	BCC DUMBO	;NOPE, SLOW DOWN
	DEC X,OBJDLY	;GIVE PLAYER CHANCE TO SEE IT
	BMI SAMURI	;GET THE KID
	LDA X,OBJHDG
	CLC
	ADC #1
	AND #3F
	STA X,OBJHDG	;CIRCLE TILL WE'RE READY
	JMP PNTVCB
SAMURI:	LDA #0FF
	STA X,OBJDLY
	LDA FRMCNT
	AND #1
	BNE TRNVCB	;EVERY OTHER FRAME
	LDA X,OBJSPD	;ACCELERATE AT HIM
	CLC
	ADC #1
	CMP #10		;FULL THROTTLE?
	BCS ZOMME
	STA X,OBJSPD
ZOMME:	LDA #0FF	;DETERMINE TARGET HEADING
	STA MINHDG
	LSR
	STA MINRNG
	JSR SMELL
	LDX HITTER
	LDA MINHDG
	AND #3F
	STA X,OBJHDG
	STA X,OBJTGT
TRNVCB:	JSR HDGFLT
PNTVCB:	JSR FLYTRN
	JSR FLYMUV
PAWSIT:	LDA X,OBJARM
	BEQ NOTRGT	;NO TARGET
	TAY		;USE TO FLASH TARGET
	LDA FRMCNT
	AND #2
	BNE NOTRGT	;EVERY OTHER FRAME FLASH
	LDA #0E7	;WHITE FLASHING ON TARGET
	STA Y,OBJVGC
NOTRGT:	JMP NXTSMT
;
; RETURNS CARRY SET IF CURRENT TARGET (Y)
; IS VALID: CARRY CLEAR IF NO GOOD
;
LEGIT:	CPY HITTER	;IS IT US?
	BEQ TGTNG	;YEP. NO GOOD
	LDA Y,OBJFLG	;IS OUR TARGET DEAD?
	AND #HEALTH
	CMP #DYING	;OUR TARGET DEAD/DYING?
	BCS TGTNG	;YEP, TRY AGAIN
	LDA Y,OBJAUX	;IS IT AN EGG? PICK ANOTHER
	AND #0F
	CMP #3		;EGG?
	BEQ TGTNG	;CAN'T GET EGGS
	CMP #5		;LEAVE $ ALONE
	BEQ TOOBAD	;DON'T GET $ TOO SOON
	CMP #8		;OR TORPEDOES
	BEQ TGTNG
	CMP #0F
	BEQ TGTNG
	CMP #0D		;DON'T PICK ON GRENADES
	BNE TGTOK
TOOBAD:	LDA WVTIME	;WE SEE IF WE CAN GET GNDS
	CMP #30.	;30 SECONDS INTO WAVE?
	BCS TGTOK	;AFTER 30 SECS GET THEM TOO
TGTNG:	CLC
	RTS
TGTOK:	SEC
	RTS
;
;
IOBJC:	JMP NXTSMT	;SPARE
;
; ESCAPE IS USED TO LET ALL OBJECTS LEAVE
; THE PLAYFIELD UPON PLAYER DEATH
;
ESCAPE:	LDA DIED	;IS PLAYER DYING?
	ORA WALKIN
	ORA OBJFLG
	BNE LEAVE	;YEP: EVERYBODY EXITS STUCK OR NOT
	LDA X,OBJFLG	;TEST OUR STUCK BIT
	AND #HEALTH
	CMP #STUCK	;CAN MEAN DIFFERENT THINGS
	BNE NOSTIK	;NORMAL NON-STUCK INTELLIGENCE
	LDA X,OBJAUX
	AND #0F
	TAY
	LDA Y,OBJTYP
	AND #8		;STUCK STATE BIT
	BNE LEAVE	;1 MEANS STUNNED EXITS
	LDA FRMCNT	;TWINKLE VIA FRAME COUNTER
	AND #7
	BNE TWINK
	LDA Y,OBJTYP	;DO WE RECOVER?
	AND #2		;NZ MEANS NO
	BNE NOFREE
	DEC X,OBJDLY	;RECOVERY TIMER
NOFREE:	TYA		;ALTERNATING COLORS
	ASL
	TAY
	LDA Y,OBJCLR
	STA X,OBJVGC+6
	LDA Y,OBJCLR+1
	STA X,OBJVGC
	LDA X,OBJDLY
	BPL TWINK	;TWINKLE WHILE STUNNED
	LDA #0		;RETURN TO ACTIVE STATE
	STA X,OBJFLG
	STA X,OBJDLY
NOSTIK:	RTS		;RETURN: NO LONGER STUNNED
TWINK:	PLA		;DISCARD RETURN ADDRESS
	PLA
TWINKL:	LDA X,OBJDLY
	CMP #8		;WIGGLE WHEN CLOSE
	BCS NOWIGL	;NO WIGGLE
	JSR WIGGLE
NOWIGL:	JMP NXTSMT
;
LEAVE:	PLA		;DISCARD RETURN
	PLA		;ADDRESS
	LDA #0		;CLEAR NO MOVEMENT
	STA X,OBJFLG
GOAWAY:	LDA FRMCNT
	AND #3
	BNE TRNXIT
	LDY #0
	LDA #0FF
	STA MINHDG	;AVOID THE PLAYER AS WE LEAVE
	LSR
	STA MINRNG
	JSR SMELL
	LDX HITTER
	CLC
	ADC #20
	AND #3F
	STA X,OBJTGT
	LDA X,OBJSPD
	CLC
	ADC #1
	CMP #10
	BCS TRNXIT
	STA X,OBJSPD	;ACCELERATE WHILE LEAVING
TRNXIT:	JSR HDGFLT
	JSR FLYTRN
	JSR FLYMUV
	JMP NXTSMT
;
;
; THE FOLLOWING TABLE SPECIFIES OBJECT
; ATTRIBUTES BY OBJECT TYPE
;
; BIT #	MEANING
;
; 0	NEVER BOUNCES OFF BOUNDARY
; 1	DOESN'T RECOVER FROM STUNNED
; 2	BEETLE/HORNET BAIT IF STUNNED
; 3	IF 'STUCK' LEAVES SCREEN FOREVER
; 4	SHOULD BE SHOT BY ATTRACT-MODE PLAYER
; 5	NOT COUNTED AS TARGET <BADGUY>
; 6	IMMUNE TO WALLS
; 7	EXPLODES WHEN HIT (NO SEE POINTS)
;
OBJTYP:	.BYTE ^B00010110	;MOSQUITO
	.BYTE ^B00100000	;PLAYER
	.BYTE ^B00010100	;HORNET
	.BYTE ^B01100000	;EGG
	.BYTE ^B01100001	;BULLET
	.BYTE ^B11010000	;BONUS $ BUG
	.BYTE ^B10011001	;MUTANT
	.BYTE ^B00010100	;BEETLE
	.BYTE ^B11101011	;TORPEDO
	.BYTE ^B01011000	;T-BUG #1
	.BYTE ^B11010001	;T-BUG #2
	.BYTE ^B01110110	;CARRION BLOB OBJ #B
	.BYTE ^B00000000	;SPARE OBJ#C
	.BYTE ^B10000000	;GRENADE
	.BYTE ^B01101010	;VCB
	.BYTE ^B01101010	;FFF
;
; THIS ROUTINE IS USED FOR HORNET HUNTER
; TO FIND THE NEAREST LEGITEMATE TARGET TO EGG
; OR THE PLAYER
;
HRNSNF:	LDA #0FF
	STA MINHDG
	LSR
	STA MINRNG
	LDA #0		;IGNORE BULLETS
HRSNFR:	TAY
	PHA
	BEQ DOSPID	;TRY OUT THE PLAYER
	LDA Y,OBJFLG	;TARGET ALIVE?
	AND #HEALTH
	CMP #DYING
	BCS HNTNXT	;DEAD OR DYING
	LDA Y,OBJAUX	;SHOULD WE TRY FOR IT?
	AND #0F
	CMP #0B		;CARRION QUALIFIES
	BNE HNTNXT
	LDA EVENTS	;CAN WE LAY EGGS?
	AND #3
	BEQ HNTNXT	;NOPE
DOSPID:	LDX HITTER	;TRY THIS ONE FOR DISTANCE
	JSR SMELL
HNTNXT:	PLA
	CLC
	ADC #10
	BCC HRSNFR	;LOOK AT EM ALL
	LDA MINHDG	;GET BEARING TO OUR TARGET
	RTS
;
; BEETLES USE
; THIS ROUTINE SNIFFS OUT NEAREST VICTIM
; SOMEWHERE ON THE WEB
;
; OBJECTS 5 THRU F ARE CHECKED FOR ACTIVE
; AND STUCK
;
SNIFF:	LDA #0FF	;DUMMY RANGE
	STA MINHDG
	LSR
	STA MINRNG
	LDA #0		;SKIP OVER PLAYER & WEBS
SNIFER:	TAY
	PHA
	BEQ BYTSPD	;TRY THE PLAYER 1ST
	LDA Y,OBJFLG	;IS THIS ONE DEAD OR DYING?
	AND #HEALTH
	CMP #DYING
	BCS SNFNXT	;YEP
	LDA Y,OBJAUX
	AND #0F
	CMP #1		;ONLY GO FOR PLAYER OR $
	BEQ BYTSPD
	CMP #0B
	BNE SNFNXT
BYTSPD:	LDX HITTER
	JSR SMELL	;FIND DISTANCE & HEADING
SNFNXT:	PLA 
	CLC
	ADC #10
	BCC SNIFER	;CHECK 'EM ALL
	LDA MINHDG	;GET HIS BEARING
	RTS
;
; THIS ROUTINE SNIFFS OUT A VICTIM
; FOR ATTRACT MODE PLAYER SHOOTING
;
; OBJECTS 5 THRU F ARE CHECKED FOR ACTIVE
;
DEDEYE:	LDA #0FF	;DUMMY RANGE
	STA MINHDG
	LDA #7F
	STA MINRNG
	LDY #50
AIMPLY:	STY TEMP2
	LDA Y,OBJFLG	;IS THIS ONE DEAD?
	AND #DYING
	BNE AIMNXT
;
; IF HELP MODE GO FOR ANYTHING
;
	BIT ATRACT	;HELP MODE
	BMI 10$
	LDA Y,OBJAUX	;DO WE WANT IT?
	AND #0F
;
; TAKE OUT THESE TWO AND PLAYER PUSHES EVERYTHING OFF
; WHEN NEW LIFE WALKS IN...
;
	TAX	;SHOULD WE STEER FOR THIS
	LDA X,OBJTYP
	AND #10
	BEQ AIMNXT	;NOPE
10$:	LDX HITTER
	JSR SMELL	;FIND DISTANCE & HEADING
AIMNXT:	LDA TEMP2 
	CLC
	ADC #10
	TAY
	BNE AIMPLY	;CHECK 'EM ALL
	LDA MINHDG	;WAS SOMEONE FOUND?
	BMI STAPUT	;STEER FOR THE CENTER
	LDX HITTER
	STA X,OBJTGT	;ESTABLISH TARGET HEADING
	JSR HDGFLT
	RTS
;
; THIS ROUTINE SNIFFS OUT A VICTIM
; TRACKING OBJECTS (GRENADE, FFF, $)
;
; OBJECTS 5 THRU F ARE CHECKED FOR ACTIVE
;
POLISH:	LDA #0FF	;DUMMY RANGE
	STA MINHDG
	LSR
	STA MINRNG
	LDY #0
	LDX HITTER
	JSR SMELL	;INCLUDE THE PLAYER IN THIS
	LDX HITTER
POLAR:	LDY #50
AIMONE:	STY TEMP2
	JSR LEGIT
	BCC AIMTWO
	LDX HITTER
	JSR SMELL	;FIND DISTANCE & HEADING
AIMTWO:	LDA TEMP2 
	CLC
	ADC #10
	TAY
	BNE AIMONE	;CHECK 'EM ALL
	LDX HITTER
	LDA MINHDG
	BPL SETONE
	LDX HITTER
	LDA X,OBJHDG
	CLC
	ADC #10
	AND #3F
	STA X,OBJTGT
	JSR HDGFLT
	RTS
SETONE:	STA X,OBJTGT	;ESTABLISH TARGET HEADING
	RTS
;
; NO VALID TARGET FOUND
; SO WALK TOWARD CENTER SCREEN
;
STAPUT:	LDX HITTER
	JSR GOCNTR
	STA X,OBJTGT
	JSR HDGFLT
	RTS
;
; COMPUTE BEARING TO CENTER SCREEN
;
GOCNTR:	LDA #0
	SEC
	SBC X,OBJABX+1
	CMP #80	;SIGN EXTEND
	ROR
	STA DX
	LDA #0
	SEC
	SBC X,OBJABY+1
	CMP #80	;SIGN EXTEND
	ROR
	TAX
	LDY DX
	JSR ATAN
	AND #3F
	LDX HITTER
	RTS
;
;UPDATE AN OBJECTS HEADING TOWARD THE
;TARGET
;
HDGFLT:	LDA SPEED
	ORA #1
	STA TEMP1
HFLITE:	LDA X,OBJTGT	;SUBTRACT IT FROM DESIRED
	SEC
	SBC X,OBJHDG
	BEQ FLTXIT	;WE'RE HEADING THE RIGHT WAY ALREADY
	BMI DIFMIN	;DIF IS NEGATIVE
	CMP #21
	BCS DFMIN2	;DIF IS >180 DEGREES
TADER:	CMP TEMP1
	BCC SMLIN	;ERROR IS LESS THAN TURNING DELTA
	LDA TEMP1	;ERROR >= DELTA, USE DELTA
SMLIN:	CLC
	ADC X,OBJHDG
	AND #3F
	STA X,OBJHDG
	TAY
FLTXIT:	RTS
DFMIN2:	STA TEMP2
	LDA #40
	SEC
	SBC TEMP2
	JMP DFFR
DIFMIN:	EOR #0FF
	CLC
	ADC #1
DFFR:	CMP #21
	BCC SBR
	SEC
	SBC #20
	JMP TADER
SBR:	STA TEMP2
	CMP TEMP1
	BCC USLIT
	LDA TEMP1
	STA TEMP2
USLIT:	LDA X,OBJHDG
	SEC
	SBC TEMP2
	AND #3F
	STA X,OBJHDG
	TAY
	RTS
;
;EXPLOSION SCAL/STAT INSTRUCTIONS FOR NORMAL EXPLOSIONS
;
	.RADIX 10
EXPSIZ:	.BYTE ZWHITE!^H0E0,^H60
	.BYTE    8, 118
	.BYTE ZRED!^H0E0,^H60
	.BYTE    7, 117
	.BYTE ZTURQ!^H0E0,^H60
	.BYTE   74, 116
	.BYTE ZYELLO!^H0E0,^H60
	.BYTE   17, 116
	.BYTE ZGREEN!^H0E0,^H60
	.BYTE   89, 115
	.BYTE ZRED!^H0E0,^H60
	.BYTE   36, 115
	.BYTE ZPURPL!^H0E0,^H60
	.BYTE  113, 114
	.BYTE ZBLUE!^H0E0,^H60
	.BYTE 63,114
	.BYTE ZWHITE!^H0E0,^H060
	.BYTE 15,114
	.RADIX 16
;
; EXPLOSIONS FOR GRENADE
; DEFINED AS KILL RADIUS,4 BYTES OF SCAL/STAT LIKE EXPSIZ
;
GNDSIZ:	.BYTE 3,00,74
	.BYTE 4,40,73
	.BYTE 5,00,73
	.BYTE 7,40,72
	.BYTE 9,00,72
	.BYTE 0D,40,71	;EASY GETS THIS BIG
	.BYTE 0F,20,71	;MEDIUM GETS THIS BIG
	.BYTE 11,0,71	;HARD GETS THIS BIG
	.END
                                                                                                                                                                                                                                                                                                                                                                                                                   